<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Strict//EN"
"http://www.w3.org/TR/xhtml1/DTD/xhtml1-strict.dtd">
<html xmlns="http://www.w3.org/1999/xhtml">
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
        <style>
            @import url(/css/style.css);
            @import url(/css/feedback.css);
        </style>
        <script src="/js/jquery.js"></script>
        <script src="/js/jquery.chili-2.2.js"></script>
        <script src="/js/script.js"></script>
        <script src="/js/html2canvas.js"></script>
        <script src="/js/jsfeedback.js"></script>
        <script>
             ChiliBook.recipeFolder = "/js/chili/";  
             ChiliBook.automaticSelector = "pre";
        </script>
        <script>
            $(function(){
                $('#feedback').click(function(){
                    $('body').feedback();
                })
            });
        </script>
        <script type="text/javascript">
            var _gaq = _gaq || [];
            _gaq.push(['_setAccount', 'UA-32830659-1']);
            _gaq.push(['_trackPageview']);

            (function() {
                var ga = document.createElement('script'); ga.type = 'text/javascript'; ga.async = true;
                ga.src = ('https:' == document.location.protocol ? 'https://ssl' : 'http://www') + '.google-analytics.com/ga.js';
                    var s = document.getElementsByTagName('script')[0]; s.parentNode.insertBefore(ga, s);
            })();
        </script>
        <title>输入与输出</title>
    </head>
    <body>
        <div id="header">
            <img id="beta" alt="beta" src="/img/beta.png" />
        </div>

        <div id="main">
            <ul class="nav">
                <li class="left">  <img src="/img/prv.png"></img><a href="make-types-and-classes-for-ourselves.html">构造我们自己的Types和Typeclasses</a></li>
                <li class="center"><a href="chapters.html">Index</a></li>
                <li class="right"> <a href="functionally-solving-problems.html">函数式地思考来解决问题</a><img src="/img/nxt.png"></img></li>
            </ul>
            <a name="输入与输出"></a><h1>输入与输出</h1><img src="/img/dognap.png" style="float:right"></img><p>我们已经说明了Haskell是一个纯粹函数式语言。虽说在命令式语言中我们习惯给电脑执行一连串指令，在函数式语言中我们是用定义东西的方式进行。在Haskell中，一个函数不能改变状态，像是改变一个变量的内容。(当一个函数会改变状态，我们说这函数是有副作用的。)在Haskell中函数唯一可以做的事是根据我们给定的参数来算出的结果。如果我们用同样的参数调用两次同一个函数，它会回传相同的结果。尽管这从命令行语言的角度来看是蛮大的限制，我们已经看过它可以达成多么酷的效果。在一个命令式语言中，编程语言没办法给你任何保证在一个简单如印出几个数字的函数不会同时烧掉你的房子，绑架你的狗并刮伤你车子的烤漆。例如，当我们要做一个二元树的时候，我们并不插入一个结点来改变原有的树。由于我们无法改变状态，我们的函数实际上回传了一棵新的二元树。</p><p>函数无法改变状态的好处是它让我们促进了我们理解程序的容易度，但同时也造成了一个问题。假如说一个函数无法改变现实世界的状态，那它要如何印出它所计算的结果?毕竟要告诉我们结果的话，它必须要改变输出设备的状态(譬如说屏幕)，然后从屏幕传达到我们的脑，并改变我们心智的状态。</p><p>不要太早下结论，Haskell实际上设计了一个非常聪明的系统来处理有副作用的函数，它漂亮地将我们的程序区分成纯粹跟非纯粹两部分。非纯粹的部分负责跟键盘还有屏幕沟通。有了这区分的机制，在跟外界沟通的同时，我们还是能够有效运用纯粹所带来的好处，像是惰性求值、容错性跟模块性</p><a name="Hello, world!"></a><h2>Hello, world!</h2><img src="/img/helloworld.png" style="float:left"></img><p>到目前为止我们都是将函数加载GHCi中来测试，像是标准函式库中的一些函式。但现在我们要做些不一样的，写一个真实跟世界交互的Haskell程序。当然不例外，我们会来写个"hello world"。</p><p>现在，我们把下一行打到你熟悉的编辑器中</p><pre class="code">main = putStrLn "hello, world"</pre><p>我们定义了一个<code>main</code>，并在里面以<code>"hello, world"</code>为参数调用了<code>putStrLn</code>。看起来没什么大不了，但不久你就会发现它的奥妙。把这程序存成<code>helloworld.hs</code>。</p><p>现在我们将做一件之前没做过的事: 编译你的程序。打开你的终端并切换到包含<code>helloworld.hs</code>的目录，并输入下列指令。</p><pre class="code">$ ghc --make helloworld
[1 of 1] Compiling Main                 ( helloworld.hs, hellowowlrd.o )
Linking helloworld ...</pre><p>顺利的话你就会得到如上的消息，接着你便可以执行你的程序<code>./helloworld</code></p><pre class="code">$ ./helloworld
hello, world</pre><p>这就是我们第一个编译成功并印出字串到屏幕的程序。很简单吧。</p><p>让我们来看一下我们究竟做了些什么，首先来看一下<code>putStrLn</code>函数的型态</p><pre class="code">ghci> :t putStrLn
putStrLn :: String -> IO ()
ghci> :t putStrLn "hello, world"
putStrLn "hello, world" :: IO ()</pre><p>我们可以这么解读<code>putStrLn</code>的型态:<code>putStrLn</code>接受一个字串并回传一个I/O action，这I/O action包含了<code>()</code>的型态。(即空的tuple，或者是unit型态)。一个I/O action是一个会造成副作用的动作，常是指读取输入或输出到屏幕，同时也代表会回传某些值。在屏幕印出几个字串并没有什么有意义的回传值可言，所以这边用一个<code>()</code>来代表。</p><p>那究竟I/O action会在什么时候被触发呢?这就是<code>main</code>的功用所在。一个I/O action会在我们把它绑定到<code>main</code>这个名字并且执行程序的时候触发。</p><p>把整个程序限制在只能有一个I/O action看似是个极大的限制。这就是为什么我们需要do表示法来将所有I/O action绑成一个。来看看下面这个例子。</p><pre class="code">main = do
    putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")</pre><p>新的语法，有趣吧! 它看起来就像一个命令式的程序。如果你编译并执行它，它便会照你预期的方式执行。我们写了一个do并且接着一连串指令，就像写个命令式程序一般，每一步都是一个I/O action。将所有I/O action用do绑在一起变成了一个大的I/O action。这个大的I/O action的型态是<code>IO ()</code>，这完全是由最后一个I/O action所决定的。</p><p>这就是为什么<code>main</code>的型态永远都是<code>main :: IO something</code>，其中<code>something</code>是某个具体的型态。按照惯例，我们通常是不会把<code>main</code>的型态在程序中写出来。</p><p>另一个有趣的事情是第三行<code>name <- getLine</code>。它看起来像是从输入读取一行并存到一个变量<code>name</code>之中。真的是这样吗? 我们来看看<code>getLine</code>的型态吧</p><pre class="code">ghci> :t getLine
getLine :: IO String</pre><img src="/img/luggage.png" style="float:left"></img><p>我们可以看到<code>getLine</code>是一个回传<code>String</code>的I/O action。因为它会等用户输入某些字串，这很合理。那<code>name <- getLine</code>又是如何?你能这样解读它: 执行一个I/O action<code>getLine</code>并将它的结果绑定到<code>name</code>这个名字。<code>getLine</code>的型态是<code>IO String</code>，所以<code>name</code>的型态会是<code>String</code>。你能把I/O action想程序一个长了脚的盒子，它会跑到真实世界中替你做某些事，像是在墙壁上涂鸦，然后带回来某些数据。一旦它带了某些数据给你，打开盒子的唯一办法就是用<code><-</code>。而且如果我们要从I/O action拿出某些数据，就一定同时要在另一个I/O action中。这就是Haskell如何漂亮地分开纯粹跟不纯粹的程序的方法。<code>getLine</code>在这样的意义下是不纯粹的，因为执行两次的时候它没办法保证会回传一样的值。这也是为什么它需要在一个<code>IO</code>的型态建构子中，那样我们才能在I/O actio中取出数据。而且任何一段程序一旦依赖着I/O数据的话，那段程序也会被视为I/O code。</p><p>但这不表示我们不能在纯粹的代码中使用I/O action回传的数据。只要我们绑定它到一个名字，我们便可以暂时地使用它。像在<code>name <-getLine</code>中<code>name</code>不过是一个普通字串，代表在盒子中的内容。我们能将这个普通的字串传给一个极度复杂的函数，并回传你一生会有多少财富。像是这样。</p><pre class="code">main = do
    putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn $ "Read this carefully, because this is your future: " ++ tellFortune name</pre><p><code>tellFortune</code>并不知道任何I/O有关的事，它的型态只不过是<code>String -> String</code>。</p><p>再来看看这段代码吧，他是合法的吗?</p><pre class="code">nameTag = "Hello, my name is " ++ getLine</pre><p>如果你回答不是，恭喜你。如果你说是，你答错了。这么做不对的理由是<code>++</code>要求两个参数都必须是串列。他左边的参数是<code>String</code>，也就是<code>[Char]</code>。然而<code>getLine</code>的型态是<code>IO String</code>。你不能串接一个字串跟I/O action。我们必须先把<code>String</code>的值从I/O action中取出，而唯一可行的方法就是在I/O action中使用<code>name <- getLine</code>。如果我们需要处理一些非纯粹的数据，那我们就要在非纯粹的环境中做。所以我们最好把I/O的部分缩减到最小的比例。</p><p>每个I/O action都有一个值封装在里面。这也是为什么我们之前的程序可以这么写。</p><pre class="code">main = do
    foo <- putStrLn "Hello, what's your name?"
    name <- getLine
    putStrLn ("Hey " ++ name ++ ", you rock!")</pre><p>然而，<code>foo</code>只会有一个<code>()</code>的值，所以绑定到<code>foo</code>这个名字似乎是多余的。另外注意到我们并没有绑定最后一行的<code>putStrLn</code>给任何名字。那是因为在一个do block中，最后一个action不能绑定任何名字。我们在之后讲解Monad的时候会说明为什么。现在你可以先想成do block会自动从最后一个action取出值并绑定给他的结果。</p><p>除了最后一行之外，其他在do中没有绑定名字的其实也可以写成绑定的形式。所以<code>putStrLn "BLAH"</code>可以写成<code>_ <- putStrLn "BLAH"</code>。但这没什么实际的意义，所以我们宁愿写成<code>putStrLn something</code>。</p><p>初学者有时候会想错</p><pre class="code">name = getLine</pre><p>以为这行会读取输入并给他榜定一个名字叫<code>name</code>但其实只是把<code>getLine</code>这个I/O action指定一个名字叫<code>name</code>罢了记住，要从一个I/O action中取出值，你必须要在另一个I/O action中将他用<code><-</code>绑定给一个名字。</p><p>I/O actions只会在绑定给<code>main</code>的时候或是在另一个用do串起来的I/O action才会执行。你可以用do来串接I/O actions，再用do来串接这些串接起来的I/O actions。不过只有最外面的I/O action被指定给main才会触发执行。</p><p>喔对，其实还有另外一个情况。就是在GHCi中输入一个I/O action并按下Enter键，那也会被执行</p><pre class="code">ghci> putStrLn "HEEY"
HEEY</pre><p>就算我们只是在GHCi中打几个数字或是调用一个函数，按下Enter就会计算他并调用<code>show</code>，他会用<code>putStrLn</code>将字串印出的终端上。</p><p>还记得let binding吗? 如果不记得，回去温习一下这个章节。他们的形式是<code>let bindings in expression</code>，其中<code>bindings</code>是expression中的名字、<code>expression</code>则是被运用到这些名字的算式。我们也提到了list comprehensions中，<code>in</code>不是必须的。你能够在do blocks中使用let bindings就像是你在list comprehensions中使用他们一样，像这样:</p><pre class="code">import Data.Char

main = do
    putStrLn "What's your first name?"
    firstName <- getLine
    putStrLn "What's your last name?"
    lastName <- getLine
    let bigFirstName = map toUpper firstName
        bigLastName = map toUpper lastName
    putStrLn $ "hey " ++ bigFirstName ++ " " ++ bigLastName ++ ", how are you?"</pre><p>注意我们是怎么编排在do block中的I/O actions，也注意到我们是怎么编排let跟其中的名字的，由于对齐在Haskell中并不会被无视，这么编排才是好的习惯。我们的程序用<code>map toUpper firstName</code>将<code>"John"</code>转成大写的<code>"JOHN"</code>，并将大写的结果绑定到一个名字上，之后在输出的时候参考到了这个名字。</p><p>你也许会问究竟什么时候要用<code><-</code>什么时候用let bindings?记住，<code><-</code>是用来运算I/O actions并将他的结果绑定到名称。而<code>map toUpper firstName</code>并不是一个I/O action。他只是一个纯粹的expression。所以总结来说，当你要绑定I/O actions的结果时用<code><-</code>，而对于纯粹的expression使用let bindings。对于错误的<code>let firstName = getLine</code>，我们只不过是把<code>getLine</code>这个I/O actions给了一个不同的名字罢了。最后还是要用<code><-</code>将结果取出。</p><p>现在我们来写一个不断读取一行输入并将这行输入的字反过来输出到屏幕上。程序会在输入空白行的时候停止。</p><pre class="code">main = do
    line <- getLine
    if null line
        then return ()
        else do
            putStrLn $ revverseWords line
            main

reverseWords :: String -> String
reverseWords = unwords . map reverse . words</pre><p>你可以执行看看来得到些感觉。</p><p>首先，我们来看一下<code>reverseWords</code>。他不过是一个普通的函数，假如接受了个字串<code>"hey there man"</code>，他会先调用<code>words</code>来产生一个字的串列<code>["hey", "there", "man"]</code>。然后用<code>reverse</code>来map整个串列，得到<code>["yeh", "ereht", "nam"]</code>，接着用<code>unwords</code>来得到最终的结果<code>"yeh ereht nam"</code>。这些用函数合成来简洁的表达。如果没有用函数合成，那就会写成丑丑的样子<code>reverseWords st = unwords (map reverse (words st))</code></p><p>那<code>main</code>又是怎么一回事呢?首先，我们用<code>getLine</code>从终端读取了一行，并把这行输入取名叫<code>line</code>。然后接着一个条件式expression。记住，在Haskell中if永远要伴随一个else，这样每个expression才会有值。当if的条件是true (也就是输入了一个空白行)，我们便执行一个I/O action，如果if的条件是false，那else底下的I/O action被执行。这也就是说当if在一个I/O do block中的时候，长的样子是<code>if condition then I/O action else I/O action</code></p><p>我们首先来看一下在else中发生了什么事。由于我们在else中只能有一个I/O action，所以我们用do来将两个I/O actions绑成一个，你可以写成这样</p><pre class="code">else (do
    putStrLn $ reverseWords line
    main)</pre><p>这样可以明显看到整个do block可以看作一个I/O action，只是比较丑。但总之，在do block里面，我们依序调用了<code>getLine</code>以及<code>reverseWords</code>，在那之后，我们递归调用了<code>main</code>。由于main也是一个I/O action，所以这不会造成任何问题。调用<code>main</code>也就代表我们回到程序的起点。</p><p>那假如<code>null line</code>的结果是true呢?也就是说then的区块被执行。我们看一下区块里面有<code>then return ()</code>。如果你是从C, Java或Python过来的，你可能会想<code>return</code>不过是作一样的事情便跳过这一段。但很重要的:<code>return</code>在Hakell里面的意义跟其他语言的<code>return</code>完全不同！他们有相同的样貌，造成了许多人搞错，但确实他们是不一样的。在命令式语言中，<code>return</code>通常结束method或subroutine的执行，并且回传某个值给调用者。在Haskell中，他的意义则是利用某个pure value造出I/O action。用之前盒子的比喻来说，就是将一个value装进箱子里面。产生出的I/O action并没有作任何事，只不过将value包起来而已。所以在I/O的情况下来说，<code>return "haha"</code>的型态是<code>IO String</code>。将pure value包成I/O action有什么实质意义呢？为什么要弄成<code>IO</code>包起来的值？这是因为我们一定要在else中摆上某些I/O action，所以我们才用<code>return ()</code>做了一个没作什么事情的I/O action。</p><p>在I/O do block中放一个<code>return</code>并不会结束执行。像下面这个程序会执行到底。</p><pre class="code">main = do
    return ()
    return "HAHAHA"
    line <- getLine
    return "BLAH BLAH BLAH"
    return 4
    putStrLn line</pre><p>所有在程序中的<code>return</code>都是将value包成I/O actions，而且由于我们没有将他们绑定名称，所以这些结果都被忽略。我们能用<code><-</code>与<code>return</code>来达到绑定名称的目的。</p><pre class="code">main = do
    a <- return "hell"
    b <- return "yeah!"
    putStrLn $ a ++ " " ++ b</pre><p>可以看到<code>return</code>与<code><-</code>作用相反。<code>return</code>把value装进盒子中，而<code><-</code>将value从盒子拿出来，并绑定一个名称。不过这么作是有些多余，因为你可以用let bindings来绑定</p><pre class="code">main = do
    let a = "hell"
        b = "yeah"
    putStrLn $ a ++ " " ++ b</pre><p>在I/O do block中需要<code>return</code>的原因大致上有两个，一个是我们需要一个什么事都不做的I/O action，或是我们不希望这个do block形成的I/O action的结果值是这个block中的最后一个I/O action，我们希望有一个不同的结果值，所以我们用<code>return</code>来作一个I/O action包了我们想要的结果放在do block的最后。</p><p>在我们接下去讲文件之前，让我们来看看有哪些实用的函数可以处理I/O。</p><p><code>putStr</code>跟<code>putStrLn</code>几乎一模一样，都是接受一个字串当作参数，并回传一个I/O action印出字串到终端上，只差在<code>putStrLn</code>会换行而<code>putStr</code>不会罢了。</p><pre class="code">main = do putStr "Hey, "
          putStr "I'm "
          putStrLn "Andy!"</pre><pre class="code">$ runhaskell putstr_test.hs
Hey, I'm Andy!</pre><p>他的type signature是<code>putStr :: String -> IO ()</code>，所以是一个包在I/O action中的unit。也就是空值，没有办法绑定他。</p><p><code>putChar</code>接受一个字符，并回传一个I/O action将他印到终端上。</p><pre class="code">main = do putChar 't'    
          putChar 'e'
          putChar 'h'</pre><pre class="code">$ runhaskell putchar_test.hs
teh</pre><p><code>putStr</code>实际上就是<code>putChar</code>递归定义出来的。<code>putStr</code>的边界条件是空字串，所以假设我们印一个空字串，那他只是回传一个什么都不做的I/O action，像<code>return ()</code>。如果印的不是空字串，那就先用<code>putChar</code>印出字串的第一个字符，然后再用<code>putStr</code>印出字串剩下部份。</p><pre class="code">putStr :: String -> IO ()
putStr [] = return ()
putStr (x:xs) = do
    putChar x
    putStr xs</pre><p>看看我们如何在I/O中使用递归，就像我们在pure code中所做的一样。先定义一个边界条件，然后再思考剩下如何作。</p><p><code>print</code>接受任何是<code>Show</code>typeclass的instance的型态的值，这代表我们知道如何用字串表示他，调用<code>show</code>来将值变成字串然后将其输出到终端上。基本上，他就是<code>putStrLn . show</code>。首先调用<code>show</code>然后把结果喂给<code>putStrLn</code>，回传一个I/O action印出我们的值。</p><pre class="code">main = do print True
          print 2
          print "haha"
          print 3.2
          print [3,4,3]</pre><pre class="code">$ runhaskell print_test.hs
True
2
"haha"
3.2
[3,4,3]</pre><p>就像你看到的，这是个很方便的函数。还记得我们提到I/O actions只有在<code>main</code>中才会被执行以及在GHCI中运算的事情吗？当我们用键盘打了些值，像<code>3</code>或<code>[1,2,3]</code>并按下Enter，GHCI实际上就是用了<code>print</code>来将这些值输出到终端。</p><pre class="code">ghci> 3
3
ghci> print 3
3
ghci> map (++"!") ["hey","ho","woo"]
["hey!","ho!","woo!"]
ghci> print (map (++"!") ["hey", "ho", "woo"])
["hey!","ho!","woo!"]</pre><p>当我们需要印出字串，我们会用<code>putStrLn</code>，因为我们不想要周围有括号，但对于输出值来说，<code>print</code>才是最常用的。</p><p><code>getChar</code>是一个从输入读进一个字符的I/O action，因此他的type signature是<code>getChar :: IO Char</code>，代表一个I/O action的结果是<code>Char</code>。注意由于缓冲区的关系，只有当Enter被按下的时候才会触发读取字符的行为。</p><pre class="code">main = do
    c <- getChar
    if c /= ' '
        then do
            putChar c
            main
        else return ()</pre><p>这程序看起来像是读取一个字符并检查他是否为一个空白。如果是的话便停止，如果不是的话便印到终端上并重复之前的行为。在某种程度上来说也不能说错，只是结果不如你预期而已。来看看结果吧。</p><pre class="code">$ runhaskell getchar_test.hs
hello sir
hello</pre><p>上面的第二行是输入。我们输入了<code>hello sir</code>并按下了Enter。由于缓冲区的关系，程序是在我们按了Enter后才执行而不是在某个输入字符的时候。一旦我们按下了Enter，那他就把我们直到目前输入的一次做完。</p><p><code>when</code>这函数可以在<code>Control.Monad</code>中找到他 (你必须<code>import Contorl.Monad</code>才能使用他)。他在一个do block中看起来就像一个控制流程的statement，但实际上他的确是一个普通的函数。他接受一个boolean值跟一个I/O action。如果boolean值是<code>True</code>，便回传我们传给他的I/O action。如果boolean值是<code>False</code>，便回传<code>return ()</code>，即什么都不做的I/O action。我们接下来用<code>when</code>来改写我们之前的程序。</p><pre class="code">import Control.Monad

main = do
    c <- getChar
    when (c /= ' ') $ do
        putChar c
        main</pre><p>就像你看到的，他可以将<code>if something then do some I/O action else return ()</code>这样的模式封装起来。</p><p><code>sequence</code>接受一串I/O action，并回传一个会依序执行他们的I/O action。运算的结果是包在一个I/O action的一连串I/O action的运算结果。他的type signature是<code>sequence :: [IO a] -> IO [a]</code></p><pre class="code">main = do
    a <- getLine
    b <- getLine
    c <- getLine
    print [a,b,c]</pre><p>其实可以写成</p><pre class="code">main = do
    rs <- sequence [getLine, getLine, getLine]
    print rs</pre><p>所以<code>sequence [getLine, getLine, getLine]</code>作成了一个执行<code>getLine</code>三次的I/O action。如果我们对他绑定一个名字，结果便是这串结果的串列。也就是说，三个用户输入的东西组成的串列。</p><p>一个常见的使用方式是我们将<code>print</code>或<code>putStrLn</code>之类的函数map到串列上。<code>map print [1,2,3,4]</code>这个动作并不会产生一个I/O action，而是一串I/O action，就像是<code>[print 1, print 2, print 3, print 4]</code>。如果我们将一串I/O action变成一个I/O action，我们必须用<code>sequence</code></p><pre class="code">ghci> sequence (map print [1,2,3,4,5])    
1
2
3
4
5
[(),(),(),(),()]</pre><p>那<code>[(),(),(),(),()]</code>是怎么回事？当我们在GHCI中运算I/O action，他会被执行并把结果印出来，唯一例外是结果是<code>()</code>的时候不会被印出。这也是为什么<code>putStrLn "hehe"</code>在GHCI中只会印出<code>hehe</code>(因为<code>putStrLn "hehe"</code>的结果是<code>()</code>)。但当我们使用<code>getLine</code>时，由于<code>getLine</code>的型态是<code>IO String</code>，所以结果会被印出来。</p><p>由于对一个串列map一个回传I/O action的函数，然后再sequence他这个动作太常用了。所以有一些函数在函式库中<code>mapM</code>跟<code>mapM_</code>。<code>mapM</code>接受一个函数跟一个串列，将对串列用函数map然后sequence结果。<code>mapM_</code>也作同样的事，只是他把运算的结果丢掉而已。在我们不关心I/O action结果的情况下，<code>mapM_</code>是最常被使用的。</p><pre class="code">ghci> mapM print [1,2,3]    
1
2
3
[(),(),()]
ghci> mapM_ print [1,2,3]
1
2
3</pre><p><code>forever</code>接受一个I/O action并回传一个永远作同一件事的I/O action。你可以在<code>Control.Monad</code>中找到他。下面的程序会不断地要用户输入些东西，并把输入的东西转成大写输出到屏幕上。</p><pre class="code">import Control.Monad
import Data.Char

main = forever $ do
    putStr "Give me some input: "
    l <- getLine
    putStrLn $ map toUpper l</pre><p>在<code>Control.Monad</code>中的<code>forM</code>跟<code>mapM</code>的作用一样，只是参数的顺序相反而已。第一个参数是串列，而第二个则是函数。这有什么用？在一些有趣的情况下还是有用的：</p><pre class="code">import Control.Monad

main = do 
    colors <- forM [1,2,3,4] (\a -> do
        putStrLn $ "Which color do you associate with the number " ++ show a ++ "?"
        color <- getLine
        return color)
    putStrLn "The colors that you associate with 1, 2, 3 and 4 are: "
    mapM putStrLn colors</pre><p><code>(\a -> do ...)</code>是接受一个数字并回传一个I/O action的函数。我们必须用括号括住他，不然lambda会贪心match的策略会把最后两个I/O action也算进去。注意我们在do block里面<code>return color</code>。我们那么作是让do block的结果是我们选的颜色。实际上我们并不需那么作，因为<code>getLine</code>已经达到我们的目的。先<code>color <- getLine</code>再<code>return color</code>只不过是把值取出再包起来，其实是跟<code>getLine</code>效果相当。<code>forM</code>产生一个I/O action，我们把结果绑定到<code>colors</code>这名称。<code>colors</code>是一个普通包含字串的串列。最后，我们用<code>mapM putStrLn colors</code>印出所有颜色。</p><p>你可以把<code>forM</code>的意思想成将串列中的每个元素作成一个I/O action。至于每个I/O action实际作什么就要看原本的元素是什么。然后，执行这些I/O action并将结果绑定到某个名称上。或是直接将结果忽略掉。</p><pre class="code">$ runhaskell from_test.hs
Which color do you associate with the number 1?  
white  
Which color do you associate with the number 2?  
blue  
Which color do you associate with the number 3?  
red  
Which color do you associate with the number 4?  
orange  
The colors that you associate with 1, 2, 3 and 4 are:  
white  
blue  
red  
orange</pre><p>其实我们也不是一定要用到<code>forM</code>，只是用了<code>forM</code>程序会比较容易理解。正常来讲是我们需要在map跟sequence的时候定义I/O action的时候使用<code>forM</code>，同样地，我们也可以将最后一行写成<code>forM colors putStrLn</code></p><p>在这一节，我们学会了输入与输出的基础。我们也了解了什么是I/O action，他们是如何帮助我们达成输入与输出的目的。这边重复一遍，I/O action跟其他Haskell中的value没有两样。我们能够把他当参数传给函式，或是函式回传I/O action。他们特别之处在于当他们是写在<code>main</code>里面或GHCI里面的时候，他们会被执行，也就是实际输出到你屏幕或输出音效的时候。每个I/O action也能包着一个从真实世界拿回来的值。</p><p>不要把像是<code>putStrLn</code>的函式想成接受字串并输出到屏幕。要想成一个函式接受字串并回传一个I/O action。当I/O action被执行的时候，会漂亮地印出你想要的东西。</p><a name="文件与字符流"></a><h2>文件与字符流</h2><img src="/img/streams.png" style="float:right"></img><p><code>getChar</code>是一个读取单一字符的I/O action。<code>getLine</code>是一个读取一行的I/O action。这是两个非常直觉的函式，多数编程语言也有类似这两个函式的statement或function。但现在我们来看看<b>getContents</b>。<code>getContents</code>是一个从标准输入读取直到end-of-file字符的I/O action。他的型态是<code>getContents :: IO String</code>。最酷的是<code>getContents</code>是惰性I/O (Lazy I/O)。当我们写了<code>foo <- getContents</code>，他并不会马上读取所有输入，将他们存在memory里面。他只有当你真的需要输入数据的时候才会读取。</p><p>当我们需要重导一个程序的输出到另一个程序的输入时，<code>getContents</code>非常有用。假设我们有下面一个文本档。</p><pre class="code">I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless</pre><p>还记得我们介绍<code>forever</code>时写的小程序吗？会把所有输入的东西转成大写的那一个。为了防止你忘记了，这边再重复一遍。</p><pre class="code">import Control.Monad  
import Data.Char  
  
main = forever $ do  
    putStr "Give me some input: "  
    l <- getLine  
    putStrLn $ map toUpper l</pre><p>将我们的程序存成<code>capslocker.hs</code>然后编译他。然后用Unix的Pipe将文本档喂给我们的程序。我们使用的是GNU的cat，会将指定的文件输出到屏幕。</p><pre class="code">$ ghc --make capslocker   
[1 of 1] Compiling Main             ( capslocker.hs, capslocker.o )  
Linking capslocker ...  
$ cat haiku.txt  
I'm a lil' teapot  
What's with that airplane food, huh?  
It's so small, tasteless  
$ cat haiku.txt | ./capslocker  
I'M A LIL' TEAPOT  
WHAT'S WITH THAT AIRPLANE FOOD, HUH?  
IT'S SO SMALL, TASTELESS  
capslocker <stdin>: hGetLine: end of file</pre><p>就如你看到的，我们是用<code>|</code>这符号来将某个程序的输出piping到另一个程序的输入。我们做的事相当于run我们的capslocker，然后将haiku的内容用键盘打到终端上，最后再按Ctrl-D来代表end-of-file。这就像执行cat haiku.txt后大喊，嘿，不要把内容印到终端上，把内容塞到capslocker!</p><p>我们用<code>forever</code>在做的事基本上就是将输入经过转换后变成输出。用<code>getContents</code>的话可以让我们的程序更加精练。</p><pre class="code">import Data.Char

main = do
    contents <- getContents
    putStr (map toUpper contents)</pre><p>我们将<code>getContents</code>取回的字串绑定到<code>contents</code>。然后用<code>toUpper</code>map到整个字串后印到终端上。记住字串基本上就是一串惰性的串列(list)，同时<code>getContents</code>也是惰性I/O，他不会一口气读入内容然后将内容存在记忆体中。实际上，他会一行一行读入并输出大写的版本，这是因为输出才是真的需要输入的数据的时候。</p><pre class="code">$ cat haiku.txt | ./capslocker
I'M A LIL' TEAPOT
WHAT'S WITH THAT AIRPLAN FOOD, HUH?
IT'S SO SMALL, TASTELESS</pre><p>很好，程序运作正常。假如我们执行capslocker然后自己打几行字呢？</p><pre class="code">$ ./capslocker
hey ho
HEY HO
lets go
LETS GO</pre><p>按下Ctrl-D来离开环境。就像你看到的，程序是一行一行将我们的输入印出来。当<code>getContent</code>的结果被绑定到<code>contents</code>的时候，他不是被表示成在记忆体中的一个字串，反而比较像是他有一天会是字串的一个承诺。当我们将<code>toUpper</code>map到<code>contents</code>的时候，便也是一个函数被承诺将会被map到内容上。最后<code>putStr</code>则要求先前的承诺说，给我一行大写的字串吧。实际上还没有任何一行被取出，所以便跟<code>contents</code>说，不如从终端那边取出些姿串吧。这才是<code>getContents</code>真正从终端读入一行并把这一行交给程序的时候。程序便将这一行用<code>toUpper</code>处里并交给<code>putStr</code>，<code>putStr</code>则印出他。之后<code>putStr</code>再说：我需要下一行。整个步骤便再重复一次，直到读到end-of-file为止。</p><p>接着我们来写个程序，读取输入，并只印出少于十个字符的行。</p><pre class="code">main = do
    contents <- getContents
    putStr (shortLinesOnly contents)

shortLinesOnly :: String -> String
shortLinesOnly input = 
    let allLines = lines input
        shortLines = filter (\line -> length line < 10) allLines
        result = unlines shortLines
    in result</pre><p>我们把I/O部份的代码弄得很短。由于程序的行为是接某些输入，作些处里然后输出。我们可以把他想成读取输入，调用一个函数，然后把函数的结果输出。</p><p><code>shortLinesOnly</code>的行为是这样：拿到一个字串，像是<code>"short\nlooooooooooooooong\nshort again"</code>。这字串有三行，前后两行比较短，中间一行很常。他用<code>lines</code>把字串分成<code>["short", "looooooooooooooong", "short again"]</code>，并把结果绑定成<code>allLines</code>。然后过滤这些字串，只有少于十个字符的留下，<code>["short", "short again"]</code>，最后用<code>unlines</code>把这些字串用换行接起来，形成<code>"short\nshort again"</code></p><pre class="code">i'm short  
so am i  
i am a loooooooooong line!!!  
yeah i'm long so what hahahaha!!!!!!  
short line  
loooooooooooooooooooooooooooong  
short</pre><pre class="code">$ ghc --make shortlinesonly  
[1 of 1] Compiling Main             ( shortlinesonly.hs, shortlinesonly.o )  
Linking shortlinesonly ...  
$ cat shortlines.txt | ./shortlinesonly  
i'm short  
so am i  
short</pre><p>我们把shortlines.txt的内容经由pipe送给shortlinesonly，结果就如你看到，我们只有得到比较短的行。</p><p>从输入那一些字串，经由一些转换然后输出这样的模式实在太常用了。常用到甚至建立了一个函数叫<strong>interact</strong>。<code>interact</code>接受一个<code>String -> String</code>的函数，并回传一个I/O action。那个I/O action会读取一些输入，调用提供的函数，然后把函数的结果印出来。所以我们的程序可以改写成这样。</p><pre class="code">main = interact shortLinesOnly

shortLinesOnly :: String -> String
shortLinesOnly input = 
    let allLines = lines input
        shortLines = filter (\line -> length line < 10) allLines
        result = unlines shortLines
    in result</pre><p>我们甚至可以再让代码更短一些，像这样</p><pre class="code">main = interact $ unlines . filter ((<10) . length) . lines</pre><p>看吧，我们让程序缩到只剩一行了，很酷吧！</p><p>能应用<code>interact</code>的情况有几种，像是从输入pipe进一些内容然后丢出一些结果的程序，或是从用户获取一行一行的输入，然后丢回根据那一行运算的结果，再拿取另一行。这两者的差别主要是取决于用户使用他们的方式。</p><p>我们再来写另一个程序，它不断地读取一行行并告诉我们那一行字串是不是一个回文本串(palindrome)。我们当然可以用<code>getLine</code>读取一行然后再调用<code>main</code>作同样的事。不过同样的事情可以用<code>interact</code>更简洁地达成。当使用<code>interact</code>的时候，想像你是将输入经有某些转换成输出。在这个情况当中，我们要将每一行输入转换成<code>"palindrome"</code>或<code>"not a palindrome"</code>。所以我们必须写一个函数将<code>"elephant\nABCBA\nwhatever"</code>转换成<code>not a palindrome\npalindrome\nnot a palindrome"</code>。来动手吧！</p><pre class="code">respondPalindromes contents = unlines (map \xs ->
    if isPalindrome xs then "palindrome" else "not a palindrome") (lines contents))
        where isPalindrome xs = xs == reverse xs</pre><p>再来将程序改写成point-free的形式</p><pre class="code">respondPalindromes = unlines . map (\xs ->
    if isPalindrome xs then "palindrome" else "not a palindrome") . lines
        where isPalindrome xs = xs == reverse xs</pre><p>很直觉吧！首先将<code>"elephant\nABCBA\nwhatever"</code>变成<code>["elephant", "ABCBA", "whatever"]</code>然后将一个lambda函数map它，<code>["not a palindrome", "palindrome", "not a palindrome"]</code>然后用<code>unlines</code>变成一行字串。接着</p><pre class="code">main = interact respondPalindromes</pre><p>来测试一下吧。</p><pre class="code">$ runhaskell palindrome.hs
hehe
not a palindrome
ABCBA
palindrome
cookie
not a palindrome</pre><p>即使我们的程序是把一大把字串转换成另一个，其实他表现得好像我们是一行一行做的。这是因为Haskell是惰性的，程序想要印出第一行结果时，他必须要先有第一行输入。所以一旦我们给了第一行输入，他便印出第一行结果。我们用end-of-line字符来结束程序。</p><p>我们也可以用pipe的方式将输入喂给程序。假设我们有这样一个文件。</p><pre class="code">dogaroo
radar
rotor
madam</pre><p>将他存为<code>words.txt</code>，将他喂给程序后得到的结果</p><pre class="code">$ cat words.txt | runhaskell palindromes.hs
not a palindrome
palindrome
palindrome
palindrome</pre><p>再一次地提醒，我们得到的结果跟我们自己一个一个字打进输入的内容是一样的。我们看不到<code>palindrome.hs</code>输入的内容是因为内容来自于文件。</p><p>你应该大致了解Lazy I/O是如何运作，并能善用他的优点。他可以从输入转换成输出的角度方向思考。由于Lazy I/O，没有输入在被用到之前是真的被读入。</p><p>到目前为止，我们的示范都是从终端读取某些东西或是印出某些东西到终端。但如果我们想要读写文件呢？其实从某个角度来说我们已经作过这件事了。我们可以把读写终端想成读写文件。只是把文件命名成<code>stdout</code>跟<code>stdin</code>而已。他们分别代表标准输出跟标准输入。我们即将看到的读写文件跟读写终端并没什么不同。</p><p>首先来写一个程序，他会开启一个叫girlfriend.txt的文件，文件里面有Avril Lavigne的畅销名曲Girlfriend，并将内容印到终端上。接下来是girlfriend.txt的内容。</p><pre class="code">Hey! Hey! You! You!   
I don't like your girlfriend!   
No way! No way!   
I think you need a new one!</pre><p>这则是我们的主程序。</p><pre class="code">import System.IO  
  
main = do  
    handle <- openFile "girlfriend.txt" ReadMode  
    contents <- hGetContents handle  
    putStr contents  
    hClose handle</pre><p>执行他后得到的结果。</p><pre class="code">$ runhaskell girlfriend.hs  
Hey! Hey! You! You!  
I don't like your girlfriend!  
No way! No way!  
I think you need a new one!</pre><p>我们来一行行看一下程序。我们的程序用do把好几个I/O action绑在一起。在do block的第一行，我们注意到有一个新的函数叫<strong>openFile</strong>。他的type signature是<code>openFile :: FilePath -> IOMode -> IO Handle</code>。他说了<code>openFile</code>接受一个文件路径跟一个<code>IOMode</code>，并回传一个I/O action，他会打开一个文件并把文件关联到一个handle。</p><p><code>FilePath</code>不过是<code>String</code>的type synonym。</p><pre class="code">type FilePath = String</pre><p><code>IOMode</code>则是一个定义如下的型态</p><pre class="code">data IOMode = ReadMode | WriteMode | AppendMode | ReadWriteMode</pre><img src="/img/file.png" style="float:left"></img><p>就像我们之前定义的型态，分别代表一个星期的七天。这个型态代表了我们想对打开的文件做什么。很简单吧。留意到我们的型态是<code>IOMode</code>而不是<code>IO Mode</code>。<code>IO Mode</code>代表的是一个I/O action包含了一个型态为<code>Mode</code>的值，但<code>IOMode</code>不过是一个阳春的enumeration。</p><p>最后，他回传一个I/O action会将指定的文件用指定的模式打开。如果我们将I/O action绑定到某个东西，我们会得到一个<code>Handle</code>。型态为<code>Handle</code>的值代表我们的文件在哪里。有了handle我们才知道要从哪个文件读取内容。想读取文件但不将文件绑定到handle上这样做是很蠢的。所以，我们将一个handle绑定到<code>handle</code>。</p><p>接着一行，我们看到一个叫<strong>hGetContents</strong>的函数。他接了一个<code>Handle</code>，所以他知道要从哪个文件读取内容并回传一个<code>IO String</code>。一个包含了文件内容的I/O action。这函数跟<code>getContents</code>差不多。唯一的差别是<code>getContents</code>会自动从标准输入读取内容（也就是终端），而<code>hGetContents</code>接了一个file handle，这file handle告诉他读取哪个文件。除此之外，他们都是一样的。就像<code>getContents</code>，<code>hGetContents</code>不会把文件一次都拉到记忆体中，而是有必要才会读取。这非常酷，因为我们把<code>contents</code>当作是整个文件般用，但他实际上不在记忆体中。就算这是个很大的文件，<code>hGetContents</code>也不会塞爆你的记忆体，而是只有必要的时候才会读取。</p><p>要留意文件的handle还有文件的内容两个概念的差异，在我们的程序中他们分别被绑订到<code>handle</code>跟<code>contents</code>两个名字。handle是我们拿来区分文件的依据。如果你把整个文件系统想成一本厚厚的书，每个文件分别是其中的一个章节，handle就像是书签一般标记了你现在正在阅读（或写入）哪一个章节，而内容则是章节本身。</p><p>我们使用<code>putStr contents</code>印出内容到标准输出，然后我们用了<strong>hClose</strong>。他接受一个handle然后回传一个关掉文件的I/O action。在用了<code>openFile</code>之后，你必须自己把文件关掉。</p><p>要达到我们目的的另一种方式是使用<strong>withFile</strong>，他的type signature是<code>withFile :: FilePath -> IOMode -> (Handle -> IO a) -> IO a</code>。他接受一个文件路径，一个<code>IOMode</code>以及一个函数，这函数则接受一个handle跟一个I/O action。<code>withFile</code>最后回传一个会打开文件，对文件作某件事然后关掉文件的I/O action。处里的结果是包在最后的I/O action中，这结果跟我们给的函数的回传是相同的。这听起来有些复杂，但其实很简单，特别是我们有lambda，来看看我们用<code>withFile</code>改写前面程序的一个范例：</p><pre class="code">import System.IO     
    
main = do     
    withFile "girlfriend.txt" ReadMode (\handle -> do  
            contents <- hGetContents handle     
            putStr contents)</pre><p>正如你看到的，程序跟之前的看起来很像。<code>(\handle -> ... )</code>是一个接受handle并回传I/O action的函数，他通常都是用lambda来表示。我们需要一个回传I/O action的函数的理由而不是一个本身作处里并关掉文件的I/O action，是因为这样一来那个I/O action不会知道他是对哪个文件在做处里。用<code>withFile</code>的话，<code>withFile</code>会打开文件并把handle传给我们给他的函数，之后他则拿到一个I/O action，然后作成一个我们描述的I/O action，最后关上文件。例如我们可以这样自己作一个<code>withFile</code>：</p><pre class="code">withFile' :: FilePath -> IOMode -> (Handle -> IO a) -> IO a  
withFile' path mode f = do  
    handle <- openFile path mode   
    result <- f handle  
    hClose handle  
    return result</pre><img src="/img/edd.png" style="float:right"></img><p>我们知道要回传的是一个I/O action，所以我们先放一个do。首先我们打开文件，得到一个handle。然后我们apply<code>handle</code>到我们的函数，并得到一个做事的I/O action。我们绑定那个I/O action到<code>result</code>这个名字，关上handle并<code>return result</code>。<code>return</code>的作用把从<code>f</code>得到的结果包在I/O action中，这样一来I/O action中就包含了<code>f handle</code>得到的结果。如果<code>f handle</code>回传一个从标准输入读去数行并写到文件然后回传读入的行数的I/O action，在<code>withFile'</code>的情形中，最后的I/O action就会包含读入的行数。</p><p>就像<code>hGetContents</code>对应<code>getContents</code>一样，只不过是针对某个文件。我们也有<strong>hGetLine</strong>,<strong>hPutStr</strong>,<strong>hPutStrLn</strong>,<b>hGetChar</b>*等等。他们分别是少了h的那些函数的对应。只不过他们他们要多拿一个handle当参数，并是针对特定文件而不是标准输出或标准输入。像是<code>putStrLn</code>是一个接受一个字串并回传一个印出加了换行字符的字串的I/O action的函数。<code>hPutStrLn</code>接受一个handle跟一个字串，回传一个印出加了换字符的字串到文件的I/O action。以此类推，<code>hGetLine</code>接受一个handle然后回传一个从文件读取一行的I/O action。</p><p>读取文件并对他们的字串内容作些处里实在太常见了，常见到我们有三个函数来更进一步简化我们的工作。</p><p><strong>readFile</strong>的type signature是<code>readFile :: FilePath -> IO String</code>。记住，<code>FilePath</code>不过是<code>String</code>的一个别名。<code>readFile</code>接受一个文件路径，回传一个惰性读取我们文件的I/O action。然后将文件的内容绑定到某个字串。他比起先<code>openFile</code>，绑定handle，然后<code>hGetContents</code>要好用多了。这边是一个用<code>readFile</code>改写之前例子的范例：</p><pre class="code">import System.IO  
  
main = do  
    contents <- readFile "girlfriend.txt"  
    putStr contents</pre><p>由于我们拿不到handle，所以我们也无法关掉他。这件事Haskell的<code>readFile</code>在背后帮我们做了。</p><p><strong>writeFile</strong>的型态是<code>writefile :: FilePath -> String -> IO ()</code>。他接受一个文件路径，以及一个要写到文件中的字串，并回传一个写入动作的I/O action。如果这个文件已经存在了，他会先把文件内容都砍了再写入。我们是反如何把girlfriend.txt的文件转成大写然后写入到girlfriendcaps.txt中</p><pre class="code">import System.IO     
import Data.Char  
    
main = do     
    contents <- readFile "girlfriend.txt"     
    writeFile "girlfriendcaps.txt" (map toUpper contents)</pre><pre class="code">$ runhaskell girlfriendtocaps.hs  
$ cat girlfriendcaps.txt  
HEY! HEY! YOU! YOU!  
I DON'T LIKE YOUR GIRLFRIEND!  
NO WAY! NO WAY!  
I THINK YOU NEED A NEW ONE!</pre><p><strong>appendFile</strong>的型态很像<code>writeFile</code>，只是<code>appendFile</code>并不会在文件存在时把文件内容砍掉而是接在后面。</p><p>假设我们有一个文件叫todo.txt，里面每一行是一件要做的事情。现在我们写一个程序，从标准输入接受一行将他加到我们的to-do list中。</p><pre class="code">import System.IO     
    
main = do     
    todoItem <- getLine  
    appendFile "todo.txt" (todoItem ++ "\n")</pre><pre class="code">$ runhaskell appendtodo.hs  
Iron the dishes  
$ runhaskell appendtodo.hs  
Dust the dog  
$ runhaskell appendtodo.hs  
Take salad out of the oven  
$ cat todo.txt  
Iron the dishes  
Dust the dog  
Take salad out of the oven</pre><p>由于<code>getLine</code>回传的值不会有换行字符，我们需要在每一行最后加上<code>"\n"</code>。</p><p>还有一件事，我们提到<code>contents <- hGetContents handle</code>是惰性I/O，不会将文件一次都读到记忆体中。</p><p>所以像这样写的话：</p><pre class="code">main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        contents <- hGetContents handle  
        putStr contents)</pre><p>实际上像是用一个pipe把文件弄到标准输出。正如你可以把list想成stream一样，你也可以把文件想成stream。他会每次读一行然后印到终端上。你也许会问这个pipe究竟一次可以塞多少东西，读去硬盘的频率究竟是多少？对于文本档而言，缺省的buffer通常是line-buffering。这代表一次被读进来的大小是一行。这也是为什么在这个case我们是一行一行处里。对于binary file而言，缺省的buffer是block-buffering。这代表我们是一个chunk一个chunk去读得。而一个chunk的大小是根据操作系统不同而不同。</p><p>你能用<code>hSetBuffering</code>来控制buffer的行为。他接受一个handle跟一个<code>BufferMode</code>，回传一个会设置buffer行为的I/O action。<code>BufferMode</code>是一个enumeration型态，他可能的值有：<code>NoBuffering</code>,<code>LineBuffering</code>或<code>BlockBuffering (Maybe Int)</code>。其中<code>Maybe Int</code>是表示一个chunck有几个byte。如果他的值是<code>Nothing</code>，则操作系统会帮你决定chunk的大小。<code>NoBuffering</code>代表我们一次读一个character。一般来说<code>NoBuffering</code>的表现很差，因为他访问硬盘的频率很高。</p><p>接下来是我们把之前的范例改写成用2048 bytes的chunk读取，而不是一行一行读。</p><pre class="code">main = do   
    withFile "something.txt" ReadMode (\handle -> do  
        hSetBuffering handle $ BlockBuffering (Just 2048)  
        contents <- hGetContents handle  
        putStr contents)</pre><p>用更大的chunk来读取对于减少访问硬盘的次数是有帮助的，特别是我们的文件其实是透过网络来访问。</p><p>我们也可以使用<strong>hFlush</strong>，他机受一个handle并回传一个会flush buffer到文件的I/O action。当我们使用line-buffering的时候，buffer在每一行都会被flush到文件。当我们使用block-buffering的时候，是在我们读每一个chunk作flush的动作。flush也会发生在关闭handle的时候。这代表当我们碰到换行字符的时候，读或写的动作都会停止并回报手边的数据。但我们能使用<code>hFlush</code>来强迫回报所有已经在buffer中的 料。经过flushing之后，数据也就能被其他程序看见。</p><p>把block-buffering的读取想成这样：你的马桶会在水箱有一加仑的水的时候自动冲水。所以你不断灌水进去直到一加仑，马桶就会自动冲水，在水里面的数据也就会被看到。但你也可以手动地按下冲水钮来冲水。他会让现有的水被冲走。冲水这个动作就是<code>hFlush</code>这个名字的含意。</p><p>我们已经写了一个将item加进to-do list里面的程序，现在我们想加进移除item的功能。我先把代码粘贴然后讲解他。我们会使用一些新面孔像是<code>System.Directory</code>以及<code>System.IO</code>里面的函数。</p><p>来看一下我们包含移除功能的程序:</p><pre class="code">import System.IO  
import System.Directory  
import Data.List  
  
main = do        
    handle <- openFile "todo.txt" ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let todoTasks = lines contents     
    numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks     
    putStrLn "These are your TO-DO items:"  
    putStr $ unlines numberedTasks  
    putStrLn "Which one do you want to delete?"     
    numberString <- getLine     
    let number = read numberString     
    newTodoItems = delete (todoTasks !! number) todoTasks     
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile "todo.txt"  
    renameFile tempName "todo.txt"</pre><p>一开始，我们用read mode打开todo.txt，并把他绑定到<code>handle</code>。</p><p>接着，我们使用了一个之前没用过在<code>System.IO</code>中的函数<strong>openTempFile</strong>。他的名字浅显易懂。他接受一个暂存的文件夹跟一个样板文件名，然后打开一个暂存盘。我们使用<code>"."</code>当作我们的暂存文件夹，因为<code>.</code>在几乎任何操作系统中都代表了现在所在的文件夹。我们使用<code>"temp"</code>当作我们暂存盘的样板名，他代表站存盘的名字会是temp接上某串随机字串。他回传一个创建暂存盘的I/O action，然后那个I/O action的结果是一个pair：暂存盘的名字跟一个handle。我们当然可以随便开启一个todo2.txt这种名字的文件。但使用<code>openTempFile</code>会是比较好的作法，这样你不会不小心覆写任何文件。</p><p>我们不用<code>getCurrentDirectory</code>的来拿到现在所在文件夹而用<code>"."</code>的原因是<code>.</code>在unix-like系统跟Windows中都表示现在的文件夹。</p><p>然后，我们绑定todo.txt的内容成<code>contents</code>。把字串断成一串字串，每个字串代表一行。<code>todoTasks</code>就变成<code>["Iron the dishes", "Dust the dog", "Take salad out of the oven"]</code>。我们用一个会把3跟<code>"hey"</code>变成<code>"3 - hey"</code>的函数，然后从0开始把这个串列zip起来。所以<code>numberedTasks</code>就是<code>["0 - Iron the dishes", "1 - Dust the dog" ...</code>。我们用<code>unlines</code>把这个串列变成一行，然后印到终端上。注意我们也有另一种作法，就是用<code>mapM putStrLn numberedTasks</code>。</p><p>我们问用户他们想要删除哪一个并且等着他们输入一个数字。假设他们想要删除1号，那代表<code>Dust the dog</code>，所以他们输入<code>1</code>。于是<code>numberString</code>就代表<code>"1"</code>。由于我们想要一个数字，而不是一个字串，所以我们用对<code>1</code>使用<code>read</code>，并且绑定到<code>number</code>。</p><p>还记得在<code>Data.List</code>中的<code>delete</code>跟<code>!!</code>吗？<code>!!</code>回传某个index的元素，而<code>delete</code>删除在串列中第一个发现的元素，然后回传一个新的没有那个元素的串列。<code>(todoTasks !! number)</code>(number代表<code>1</code>) 回传<code>"Dust the dog"</code>。我们把<code>todoTasks</code>去掉第一个<code>"Dust the dog"</code>后的串列绑定到<code>newTodoItems</code>，然后用<code>unlines</code>变成一行然后写到我们所打开的暂存盘。旧有的文件并没有变动，而暂存盘包含砍掉那一行后的所有内容。</p><p>在我们关掉源文件跟暂存盘之后我们用<strong>removeFile</strong>来移除原本的文件。他接受一个文件路径并且删除文件。删除旧得todo.txt之后，我们用<strong>renameFile</strong>来将暂存盘重命名成todo.txt。特别留意<code>removeFile</code>跟<code>renameFile</code>(两个都在<code>System.Directory</code>中)接受的是文件路径，而不是handle</p><p>这就是我们要得，实际上我们可以用更少行写出同样的程序，但我们很小心地避免覆写任何文件，并询问操作系统我们可以把暂存盘摆在哪？让我们来执行看看。</p><pre class="code">$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
Which one do you want to delete?  
1  

$ cat todo.txt  
Iron the dishes  
Take salad out of the oven  

$ runhaskell deletetodo.hs  
These are your TO-DO items:  
0 - Iron the dishes  
1 - Take salad out of the oven  
Which one do you want to delete?  
0  
  
$ cat todo.txt  
Take salad out of the oven</pre><a name="命令行引数"></a><h2>命令行引数</h2><img src="/img/arguments.png" style="float:right"></img><p>如果你想要写一个在终端里运行的程序，处里命令行引数是不可或缺的。幸运地，利用Haskell的Standard Libary能让我们有效地处里命令行引数。</p><p>在之前的章节中，我们写了一个能将to-do item加进或移除to-do list的一个程序。但我们的写法有两个问题。第一个是我们把放to-do list的文件名称给写死了。我们擅自决定用户不会有很多个to-do lists，就把文件命名为todo.txt。</p><p>一种解决的方法是每次都询问用户他们想将他们的to-do list放进哪个文件。我们在用户要删除的时候也采用这种方式。这是一种可以运作的方式，但不太能被接受，因为他需要用户运行程序，等待程序询问才能回答。这被称为交互式的程序，但讨厌的地方在当你想要自动化执行程序的时候，好比说写成script，这会让你的script写起来比较困难。</p><p>这也是为什么有时候用户在执行的时候就告诉程序他们要什么会比叫好，而不是让程序去问用户要什么。更好的是是让用户透过命令行引数告诉程序他想要什么。</p><p>在<code>System.Environment</code>模块当中有两个很酷的I/O actions，一个是<strong>getArgs</strong>，他的type是<code>getArgs :: IO [String]</code>，他是一个拿取命令行引数的I/O action，并把结果放在包含的一个串列中。<strong>getProgName</strong>的型态是<code>getProgName :: IO String</code>，他则是一个I/O action包含了程序的名称。</p><p>我们来看一个展现他们功能的程序。</p><pre class="code">import System.Environment   
import Data.List  
  
main = do  
    args <- getArgs  
    progName <- getProgName  
    putStrLn "The arguments are:"  
    mapM putStrLn args  
    putStrLn "The program name is:"  
    putStrLn progName</pre><p>我们将<code>getArgs</code>跟<code>progName</code>分别绑定到<code>args</code>跟<code>progName</code>。我们印出<code>The arguments are:</code>以及在<code>args</code>中的每个引数。最后，我们印出程序的名字。我们把程序编译成<code>arg-test</code>。</p><pre class="code">$ ./arg-test first second w00t "multi word arg"  
The arguments are:  
first  
second  
w00t  
multi word arg  
The program name is:  
arg-test</pre><p>知道了这些函数现在你能写几个很酷的命令行程序。在之前的章节，我们写了一个程序来加入待作事项，也写了另一个程序删除事项。现在我们要把两个程序合起来，他会根据命令行引数来决定该做的事情。我们也会让程序可以处里不同的文件，而不是只有todo.txt</p><p>我们叫这程序todo，他会作三件事：</p><ul><li><p>查看待作事项</p></li><li><p>加入待作事项</p></li><li><p>删除待作事项</p></li></ul><p>我们暂不考虑不合法的输入这件事。</p><p>我们的程序要像这样运作：假如我们要加入<code>Find the magic sword of power</code>，则我们会打<code>todo add todo.txt "Find the magic sword of power"</code>。要查看事项我们则会打<code>todo view todo.txt</code>，如果要移除事项二则会打<code>todo remove todo.txt 2</code></p><p>我们先作一个分发的association list。他会把命令行引数当作key，而对应的处里函数当作value。这些函数的型态都是<code>[String] -> IO ()</code>。他们会接受命令行引数的串列并回传对应的查看，加入以及删除的I/O action。</p><pre class="code">import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]</pre><p>我们定义了<code>main</code>，<code>add</code>，<code>view</code>跟<code>remove</code>，</p><p>就从<code>main</code>开始讲吧</p><pre class="code">main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  V</pre><p>首先，我们取出引数并把他们绑定到<code>(command:args)</code>。如果你还记得pattern matching，这么做会把第一个引数绑定到<code>command</code>，把其他的绑定到<code>args</code>。如果我们像这样执行程序<code>todo add todo.txt "Spank the monkey"</code>，<code>command</code>会变成<code>"add"</code>，而<code>args</code>会变成<code>["todo.xt", "Spank the monkey"]</code>。</p><p>在下一行，我们在一个分派的的串列中寻到我们的指令是哪个。由于<code>"add"</code>指向<code>add</code>，我们的结果便是<code>Just add</code>。我们再度使用了pattern matching来把我们的函数从<code>Maybe</code>中取出。但如果我们想要的指令不在分派的串列中呢？那样lookup就会回传<code>Nothing</code>，但我们这边并不特别处里失败的情况，所以pattern matching会失败然后我们的程序就会当掉。</p><p>最后，我们用剩下的引数调用<code>action</code>这个函数。他会还传一个加入item，显示所有items或者删除item的I/O action。由于这个I/O action是在<code>main</code>的do block中，他最后会被执行。如果我们的<code>action</code>函数是<code>add</code>，他就会被喂<code>args</code>然后回传一个加入<code>Spank the monkey</code>到todo.txt中的I/O action。</p><p>我们剩下要做的就是实做<code>add</code>，<code>view</code>跟<code>remove</code>，我们从<code>add</code>开始：</p><pre class="code">add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")</pre><p>如果我们这样执行程序<code>todo add todo.txt "Spank the monkey"</code>，则<code>"add"</code>会被绑定到<code>command</code>，而<code>["todo.txt", "Spank the monkey"]</code>会被带到从dispatch list中拿到的函数。</p><p>由于我们不处里不合法的输入，我们只针对这两项作pattern matching，然后回传一个附加一行到文件末尾的I/O action。</p><p>接着，我们来实做查看串列。如果我们想要查看所有items，我们会<code>todo view todo.txt</code>。所以<code>command</code>会是<code>"view"</code>，而<code>args</code>会是<code>["todo.txt"]</code>。</p><pre class="code">view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
    numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks</pre><p>这跟我们之前删除文件的程序差不多，只是我们是在显示内容而已，</p><p>最后，我们要来实做<code>remove</code>。他基本上跟之前写的只有删除功能的程序很像，所以如果你不知道删除是怎么做的，可以去看之前的解释。主要的差别是我们不写死todo.txt，而是从参数取得。我们也不会提示用户要删除哪一号的item，而是从参数取得。</p><pre class="code">remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName</pre><p>我们打开<code>fileName</code>的文件以及一个暂存。删除用户要我们删的那一行后，把文件内容写到暂存盘。砍掉原本的文件然后把暂存盘重命名成<code>fileName</code>。</p><p>来看看完整的程序。</p><pre class="code">import System.Environment   
import System.Directory  
import System.IO  
import Data.List  
  
dispatch :: [(String, [String] -> IO ())]  
dispatch =  [ ("add", add)  
            , ("view", view)  
            , ("remove", remove)  
            ]  
   
main = do  
    (command:args) <- getArgs  
    let (Just action) = lookup command dispatch  
    action args  
  
add :: [String] -> IO ()  
add [fileName, todoItem] = appendFile fileName (todoItem ++ "\n")  
  
view :: [String] -> IO ()  
view [fileName] = do  
    contents <- readFile fileName  
    let todoTasks = lines contents  
        numberedTasks = zipWith (\n line -> show n ++ " - " ++ line) [0..] todoTasks  
    putStr $ unlines numberedTasks  
  
remove :: [String] -> IO ()  
remove [fileName, numberString] = do  
    handle <- openFile fileName ReadMode  
    (tempName, tempHandle) <- openTempFile "." "temp"  
    contents <- hGetContents handle  
    let number = read numberString  
        todoTasks = lines contents  
        newTodoItems = delete (todoTasks !! number) todoTasks  
    hPutStr tempHandle $ unlines newTodoItems  
    hClose handle  
    hClose tempHandle  
    removeFile fileName  
    renameFile tempName fileName</pre><img src="/img/salad.png" style="float:left"></img><p>总结我们的程序：我们做了一个dispatch association，将指令对应到一些会接受命令行引数并回传I/O action的函数。我们知道用户下了什么命令，并根据那个命令从dispatch list取出对影的函数。我们用剩下的命令行引数调用哪些函数而得到一些作相对应事情的I/O action。然后便执行那些I/O action。</p><p>在其他编程语言，我们可能会用一个大的switch case来实做，但使用高端函数让我们可以要dispatch list给我们要的函数，并要那些函数给我们适当的I/O action。</p><p>让我们看看执行结果。</p><pre class="code">$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  

$ ./todo add todo.txt "Pick up children from drycleaners"  

$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Take salad out of the oven  
3 - Pick up children from drycleaners  

$ ./todo remove todo.txt 2  

$ ./todo view todo.txt  
0 - Iron the dishes  
1 - Dust the dog  
2 - Pick up children from drycleaners</pre><p>要再另外加新的选项也是很容易。只要在dispatch list加入新的会作你要的事情函数。你可以试试实做一个<code>bump</code>函数，接受一个文件跟一个task number，他会回传一个把那个task搬到to-do list顶端的I/O action。</p><p>对于不合法的输入你也可以让程序结束地漂亮一点。(例如用户输入了<code>todo UP YOURS HAHAHAHA</code>)可以作一个回报错误的I/O action (例如errorExist :: IO ())检查有没有不合法的输入，如果有便执行这个回报错误的I/O action。我们之后会谈另一个可能，就是用exception。</p><a name="乱数"></a><h2>乱数</h2><img src="/img/random.png" style="float:right"></img><p>在许多情况下，你写程序会需要些随机的数据。或许你在制作一个游戏，在游戏中你需要掷骰子。或是你需要测试程序的测试数据。精准一点地说，我们需要pseudo-random的数据，我们知道真正的随机数据好比是一只猴子拿着起司跟奶油骑在单轮车上，任何事情都会发生。在这个章节，我们要看看如何让Haskell产生些pseudo-random的数据。</p><p>在大多数其他的编程语言中，会给你一些函数能让你拿到些随机乱数。每调用一次他就会拿到一个不同的数字。那在Haskell中是如何？要记住Haskell是一个纯粹函数式语言。代表任何东西都具有referential transparency。那代表你喂给一个函数相同的参数，不管怎么调用都是回传相同的结果。这很新奇的原因是因为他让我们理解程序的方式不同，而且可以让我们延迟计算，直到我们真正需要他。如果我调用一个函数，我可以确定他不会乱来。我真正在乎的是他的结果。然而，这会造成在乱数的情况有点复杂。如果我有一个函数像这样：</p><pre class="code">randomNumber :: (Num a) => a
randomNumber = 4</pre><p>由于他永远回传<code>4</code>，所以对于乱数的情形而言是没什么意义。就算4这个结果是掷骰子来的也没有意义。</p><p>其他的编程语言是怎么产生乱数的呢？他们可能随便拿取一些电脑的信息，像是现在的时间，你怎么移动你的鼠标，以及周围的声音。根据这些算出一个数值让他看起来好像随机的。那些要素算出来的结果可能在每个时间都不同，所以你会拿到不同的随机数字。</p><p>所以说在Haskell中，假如我们能作一个函数，他会接受一个具随机性的参数，然后根据那些信息还传一个数值。</p><p>在<code>System.Random</code>模块中。他包含所有满足我们需求的函数。让我们先来看其中一个，就是<strong>random</strong>。他的型态是<code>random :: (RandomGen g, Random a) => g -> (a, g)</code>。哇，出现了新的typeclass。<strong>RandomGen</strong>typeclass是指那些可以当作乱源的型态。而<strong>Random</strong>typeclass则是可以装乱数的型态。一个布林值可以是随机值，不是<code>True</code>就是<code>False</code>。一个整数可以是随机的好多不同值。那你会问，函数可以是一个随机值吗？我不这么认为。如果我们试着翻译<code>random</code>的型态宣告，大概会是这样：他接受一个random generator (乱源所在)，然后回传一个随机值以及一个新的random generator。为什么他要回传一个新的random generator呢？就是下面我们要讲的。</p><p>要使用<code>random</code>函数， 我们必须要了解random generator。 在<code>System.Random</code>中有一个很酷的型态，叫做<strong>StdGen</strong>， 他是<code>RandomGen</code>的一个instance。 我们可以自己手动作一个<code>StdGen</code>也可以告诉系统给我们一个现成的。</p><p>要自己做一个random generator，要使用<strong>mkStdGen</strong>这个函数。他的型态是<code>mkStdGen :: Int -> StdGen</code>。他接受一个整数，然后根据这个整数会给一个random generator。让我们来试一下<code>random</code>以及<code>mkStdGen</code>，用他们产生一个乱数吧。</p><pre class="code">ghci> random (mkStdGen 100)</pre><pre class="code"><interactive>:1:0:  
    Ambiguous type variable `a' in the constraint:  
        `Random a' arising from a use of `random' at <interactive>:1:0-20  
    Probable fix: add a type signature that fixes these type variable(s)  `</pre><p>这是什么？由于<code>random</code>函数会回传<code>Random</code>typeclass中任何一种型态，所以我们必须告诉Haskell我们是要哪一种型态。不要忘了我们是回传random value跟random generator的一个pair</p><pre class="code">ghci> random (mkStdGen 100) :: (Int, StdGen)  
(-1352021624,651872571 1655838864)</pre><p>我们终于有了一个看起来像乱数的数字。tuple的第一个部份是我们的乱数，而第二个部份是一个新的random generator的文本表示。如果我们用相同的random generator再调用<code>random</code>一遍呢？</p><pre class="code">ghci> random (mkStdGen 100) :: (Int, StdGen)  
(-1352021624,651872571 1655838864)</pre><p>不易外地我们得到相同的结果。所以我们试试用不同的random generator作为我们的参数。</p><pre class="code">ghci> random (mkStdGen 949494) :: (Int, StdGen)  
(539963926,466647808 1655838864)</pre><p>很好，我们拿到了不同的数字。我们可以用不同的型态标志来拿到不同型态的乱数</p><pre class="code">ghci> random (mkStdGen 949488) :: (Float, StdGen)  
(0.8938442,1597344447 1655838864)  
ghci> random (mkStdGen 949488) :: (Bool, StdGen)  
(False,1485632275 40692)  
ghci> random (mkStdGen 949488) :: (Integer, StdGen)  
(1691547873,1597344447 1655838864)</pre><p>让我们写一个仿真丢三次铜板的函数。假如<code>random</code>不同时回传一个乱数以及一个新的random generator，我们就必须让这函数接受三个random generators让他们每个回传一个掷铜板的结果。但那样听起来怪怪的，加入一个generator可以产生一个型态是<code>Int</code>的乱数，他应该可以产生掷三次铜板的结果（总共才八个组合）。这就是<code>random</code>为什么要回传一个新的generator的关键了。</p><p>我们将一个铜板表示成<code>Bool</code>。<code>True</code>代表反面，<code>False</code>代表正面。</p><pre class="code">threeCoins :: StdGen -> (Bool, Bool, Bool)  
threeCoins gen =   
    let (firstCoin, newGen) = random gen  
    (secondCoin, newGen') = random newGen  
    (thirdCoin, newGen') = random newGen'  
    in  (firstCoin, secondCoin, thirdCoin)  )</pre><p>我们用我们拿来当参数的generator调用<code>random</code>并得到一个掷铜板的结果跟一个新的generator。然后我们再用新的generator调用他一遍，来得到第二个掷铜板的结果。对于第三个掷铜板的结果也是如法炮制。如果我们一直都用同样的generator，那所有的结果都会是相同的值。也就是不是<code>(False, False, False)</code>就是<code>(True, True, True)</code>。</p><pre class="code">ghci> threeCoins (mkStdGen 21)  
(True,True,True)  
ghci> threeCoins (mkStdGen 22)  
(True,False,True)  
ghci> threeCoins (mkStdGen 943)  
(True,False,True)  
ghci> threeCoins (mkStdGen 944)  
(True,True,True)</pre><p>留意我们不需要写<code>random gen :: (Bool, StdGen)</code>。那是因为我们已经在函数的型态宣告那边就表明我们要的是布林。而Haskell可以推敲出我们要的是布林值。</p><p>假如我们要的是掷四次？甚至五次呢？有一个函数叫<strong>randoms</strong>，他接受一个generator并回传一个无穷串行。</p><pre class="code">ghci> take 5 $ randoms (mkStdGen 11) :: [Int]  
[-1807975507,545074951,-1015194702,-1622477312,-502893664]  
ghci> take 5 $ randoms (mkStdGen 11) :: [Bool]  
[True,True,True,True,False]  
ghci> take 5 $ randoms (mkStdGen 11) :: [Float]  
[7.904789e-2,0.62691015,0.26363158,0.12223756,0.38291094]</pre><p>为什么<code>randoms</code>不另外多回传一个新的generator呢？我们可以这样地实做<code>randoms</code></p><pre class="code">randoms' :: (RandomGen g, Random a) => g -> [a]  
randoms' gen = let (value, newGen) = random gen in value:randoms' newGen</pre><p>一个递归的定义。我们由现在的generator拿到一个乱数跟一个新的generator，然后制作一个list，list的第一个值是那个乱数，而list的其余部份是根据新的generator产生出的其余乱数们。由于我们可能产生出无限的乱数，所以不可能回传一个新的generator。</p><p>我们可以写一个函数，他会回传有限个乱数跟一个新的generator</p><pre class="code">finiteRandoms :: (RandomGen g, Random a, Num n) => n -> g -> ([a], g)  
finiteRandoms 0 gen = ([], gen)  
finiteRandoms n gen =   
    let (value, newGen) = random gen  
        (restOfList, finalGen) = finiteRandoms (n-1) newGen  
    in  (value:restOfList, finalGen)</pre><p>又是一个递归的定义。我们说如果我们要0个乱数，我们便回传一个空的list跟原本给我们的generator。对于其他数量的乱数，我们先拿一个乱数跟一个新的generator。这一个乱数便是list的第一个数字。然后list中剩下的便是n-1个由新的generator产生出的乱数。然后我们回传整个list跟最后一个产生完n-1个乱数后generator。</p><p>如果我们要的是在某个范围内的乱数呢？现在拿到的乱数要不是太大就是太小。如果我们想要的是骰子上的数字呢？<strong>randomR</strong>能满足我们的需求。他的型态是<code>randomR :: (RandomGen g, Random a) :: (a, a) -> g -> (a, g)</code>，代表他有点类似<code>random</code>。只不过他的第一个参数是一对数目，定义了最后产生乱数的上界以及下界。</p><pre class="code">ghci> randomR (1,6) (mkStdGen 359353)  
(6,1494289578 40692)  
ghci> randomR (1,6) (mkStdGen 35935335)  
(3,1250031057 40692)</pre><p>另外也有一个<strong>randomRs</strong>的函数，他会产生一连串在给定范围内的乱数：</p><pre class="code">ghci> take 10 $ randomRs ('a','z') (mkStdGen 3) :: [Char]  
"ndkxbvmomg"</pre><p>这结果看起来像是一个安全性很好的密码。</p><p>你会问你自己，这一单元跟I/O有关系吗？到现在为止还没出现任何跟I/O有关的东西。到现在为止我们都是手动地做我们的random generator。但那样的问题是，程序永远都会回传同样的乱数。这在真实世界中的程序是不能接受的。这也是为什么<code>System.Random</code>要提供<strong>getStdGen</strong>这个I/O action，他的型态是<code>IO StdGen</code>。当你的程序执行时，他会跟系统要一个random generator，并存成一个global generator。<code>getStdGen</code>会替你拿那个global random generator并把他绑定到某个名称上。</p><p>这里有一个简单的产生随机字串的程序。</p><pre class="code">import System.Random  
  
main = do  
    gen <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen)</pre><pre class="code">$ runhaskell random_string.hs  
pybphhzzhuepknbykxhe  
$ runhaskell random_string.hs  
eiqgcxykivpudlsvvjpg  
$ runhaskell random_string.hs  
nzdceoconysdgcyqjruo  
$ runhaskell random_string.hs  
bakzhnnuzrkgvesqplrx</pre><p>要当心当我们连续两次调用<code>getStdGent</code>的时候，实际上都会回传同样的global generator。像这样：</p><pre class="code">import System.Random  
  
main = do  
    gen <- getStdGen  
    putStrLn $ take 20 (randomRs ('a','z') gen)  
    gen2 <- getStdGen  
    putStr $ take 20 (randomRs ('a','z') gen2)</pre><p>你会印出两次同样的字串。要能得到两个不同的字串是建立一个无限的stream，然后拿前20个字当作第一个字串，拿下20个字当作第二个字串。要这么做，我们需要在<code>Data.List</code>中的<code>splitAt</code>函数。他会把一个list根据给定的index切成一个tuple，tuple的第一部份就是切断的前半，第二个部份就是切断的后半。</p><pre class="code">import System.Random  
import Data.List  
  
main = do  
    gen <- getStdGen  
    let randomChars = randomRs ('a','z') gen  
        (first20, rest) = splitAt 20 randomChars  
        (second20, _) = splitAt 20 rest  
    putStrLn first20  
    putStr second20</pre><p>另一种方法是用<strong>newStdGen</strong>这个I/O action，他会把现有的random generator分成两个新的generators。然后会把其中一个指定成global generator，并回传另一个。</p><pre class="code">import System.Random  
  
main = do     
    gen <- getStdGen     
    putStrLn $ take 20 (randomRs ('a','z') gen)     
    gen' <- newStdGen  
    putStr $ take 20 (randomRs ('a','z') gen')</pre><p>当我们绑定<code>newStdGen</code>的时候我们不只是会拿到一个新的generator，global generator也会被重新指定。所以再调用一次<code>getStdGen</code>并绑定到某个名称的话，我们就会拿到跟<code>gen</code>不一样的generator。</p><p>这边有一个小程序会让用户猜数字：</p><pre class="code">import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    askForNumber gen  

askForNumber :: StdGen -> IO ()  
askForNumber gen = do  
    let (randNumber, newGen) = randomR (1,10) gen :: (Int, StdGen)  
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number   
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
            askForNumber newGen</pre><img src="/img/jackofdiamonds.png" style="float:left"></img><p>我们写了一个<code>askForNumber</code>的函数，他接受一个random generator并回传一个问用户要数字并回答是否正确的I/O action。在那个函数里面，我们先根据从参数拿到的generator产生一个乱数以及一个新的generator，分别叫他们为<code>randomNumber</code>跟<code>newGen</code>。假设那个产生的数字是<code>7</code>。则我们要求用户猜我们握有的数字是什么。我们用<code>getLine</code>来将结果绑定到<code>numberString</code>上。当用户输入<code>7</code>，<code>numberString</code>就会是<code>"7"</code>。接下来，我们用<code>when</code>来检查用户输入的是否是空字串。如果是，那一个空的I/O action<code>return ()</code>就会被回传。基本上就等于是结束程序的意思。如果不是，那I/O action就会被执行。我们用<code>read</code>来把<code>numberString</code>转成一个数字，所以<code>number</code>便会是<code>7</code>。</p><blockquote><p>如果用户给我们一些<code>read</code>没办法读取的输入（像是<code>"haha"</code>），我们的程序便会当掉并印出错误消息。 如果你不希望你的程序当掉，就用<strong>reads</strong>，当读取失败的时候他会回传一个空的list。当成功的时候他就回传一个 tuple，第一个部份是我们想要的数字，第二个部份是读取失败的字串。</p></blockquote><p>我们检查如果输入的数字跟我们随机产生的数字一样，便提示用户恰当的消息。然后再递归地调用<code>askForNumber</code>，只是会拿到一个新的generator。就像之前的generator一样，他会给我们一个新的I/O action。</p><p><code>main</code>的组成很简单，就是由拿取一个random generator跟调用<code>askForNumber</code>组成罢了。</p><p>来看看我们的程序：</p><pre class="code">$ runhaskell guess_the_number.hs  
Which number in the range from 1 to 10 am I thinking of? 4  
Sorry, it was 3  
Which number in the range from 1 to 10 am I thinking of? 10  
You are correct!  
Which number in the range from 1 to 10 am I thinking of? 2  
Sorry, it was 4  
Which number in the range from 1 to 10 am I thinking of? 5  
Sorry, it was 10  
Which number in the range from 1 to 10 am I thinking of?</pre><p>用另一种方式写的话像这样：</p><pre class="code">import System.Random  
import Control.Monad(when)  
  
main = do  
    gen <- getStdGen  
    let (randNumber, _) = randomR (1,10) gen :: (Int, StdGen)     
    putStr "Which number in the range from 1 to 10 am I thinking of? "  
    numberString <- getLine  
    when (not $ null numberString) $ do  
        let number = read numberString  
        if randNumber == number  
            then putStrLn "You are correct!"  
            else putStrLn $ "Sorry, it was " ++ show randNumber  
        newStdGen  
        main</pre><p>他非常类似我们之前的版本，只是不是递归地调用，而是把所有的工作都在<code>main</code>里面做掉。在告诉用户他们猜得是否正确之后，便更新global generator然后再一次调用<code>main</code>。两种策略都是有效但我比较喜欢第一种方式。因为他在<code>main</code>里面做的事比较少，并提供我们一个可以重复使用的函数。</p><a name="Bytestrings"></a><h2>Bytestrings</h2><img src="/img/chainchomp.png" style="float:right"></img><p>List是一种有用又酷的数据结构。到目前为止，我们几乎无处不使用他。有好几个函数是专门处里List的，而Haskell惰性的性质又让我们可以用filter跟map来替换其他语言中的for loop跟while loop。也由于evaluation只会发生在需要的时候，像infinite list也对于Haskell不成问题（甚至是infinite list of infinite list）。这也是为什么list能被用来表达stream，像是读取标准输入或是读取文件。我们可以打开文件然后读取内容成字串，即便实际上我们是需要的时候才会真正取读取。</p><p>然而，用字串来处里文件有一个缺点：就是他很慢。想你所知道的，<code>String</code>是一个<code>[Char]</code>的type synonym。<code>Char</code>没有一个固定的大小，因为他可能由好几个byte组成，好比说Unicode。再加上list是惰性的。如果你有一个list像<code>[1,2,3,4]</code>，他只会在需要的时候被evaluate。所以整个list其实比较像是一个"保证"你会有一个list。要记住<code>[1,2,3,4]</code>不过是<code>1:2:3:4:[]</code>的一个syntactic sugar。当list的第一个元素被evaluated的时候，剩余的部份<code>2:3:4:[]</code>一样也只是一个"保证"你会有一个list，以此类推。以此类推。以此类推。所以你可以想像成list是保证在你需要的时候会给你第一个元素，以及保证你会有剩下的部份当你还需要更多的时候。其实不难说服你这样做并不是一个最有效率的作法。</p><p>这样额外的负担在大多数时候不会造成困扰，但当我们要读取一个很大的文件的时候就是个问题了。这也是为什么Haskell要有<code>bytestrings</code>。Bytestrings有点像list，但他每一个元素都是一个byte (8 bits)，而且他们惰性的程度也是不同。</p><p>Bytesstrings有两种：strict跟lazy。Strict bytestrings放在<code>Data.ByteString</code>，他们把惰性的性质完全拿掉。不会有所谓任何的"保证"，一个strict bytestring就代表一连串的bytes。因此你不会有一个无限长的strict bytestrings。如果你evaluate第一个byte，你就必须evalute整个bytestring。这么做的优点是他会比较少overhaed，因为他没有"Thunk"(也就是用Haskell术语来说的"保证")。缺点就是他可能会快速消耗你的记忆体，因为你把他们一次都读进了记忆体。</p><p>另一种bytestring是放在<code>Data.ByteString.Lazy</code>中。他们具有惰性，但又不像list那么极端。就像我们之前说的，List的thunk个数是跟list中有几个元素一模一样。这也是为什么他们速度没办法满足一些特殊需求。Lazy bytestrings则用另一种作法，他们被存在chunks中(不要跟Thunk搞混)，每一个chunck的大小是64K。所以如果你evaluate lazy bytestring中的byte，则前64K会被evaluated。在那个chunck之后，就是一些"保证"会有剩余的chunk。lazy bytestrings有点像装了一堆大小为64K的strict bytestrings的list。当你用lazy bytestring处里一个文件的时候，他是一个chunk一个chunk去读。这很棒是因为他不会让我们一下使用大量的记忆体，而且64K有很高的可能性能够装进你CPU的L2 Cache。</p><p>如果你大概看过<code>Data.ByteString.Lazy</code>的文档，你会看到到他有一堆函数的名称跟<code>Data.List</code>中的函数名称相同，只是出现的type signature是<code>ByteString</code>而不是<code>[a]</code>，是<code>Word8</code>而不是<code>a</code>。同样名称的函数基本上表现的行为跟list中的差不多。因为名称是一样的，所以必须用qualified import才不会在装载进GHCI的时候造成冲突。</p><pre class="code">import qualified Data.ByteString.Lazy as B
import qualified Data.ByteString as S</pre><p><code>B</code>中有lazy bytestrings跟对应的函数，而<code>S</code>中则有strict的版本。大多数时候我们是用lazy的版本。</p><p><strong>pack</strong>函数的type signature是<code>pack :: [Word8] -> ByteString</code>。代表他接受一串型态为<code>Word8</code>的bytes，并回传一个<code>ByteString</code>。你能想像一个lazy的list，要让他稍微不lazy一些，所以让他对于64K lazy。</p><p>那<code>Word8</code>型态又是怎么一回事？。他就像<code>Int</code>，只是他的范围比较小，介于0-255之间。他代表一个8-bit的数字。就像<code>Int</code>一样，他是属于<code>Num</code>这个typeclass。例如我们知道<code>5</code>是polymorphic的，他能够表现成任何数值型态。其实<code>Word8</code>他也能表示。</p><pre class="code">ghci> B.pack [99,97,110]  
Chunk "can" Empty  
ghci> B.pack [98..120]  
Chunk "bcdefghijklmnopqrstuvwx" Empty</pre><p>正如你看到的，你其实不必特别在意<code>Word8</code>，因为型态系统会选择正确的型态。如果你试着用比较大的数字，像是<code>336</code>。那对于<code>Word8</code>他就会变成<code>80</code>。</p><p>我们把一些数值打包成<code>ByteString</code>，使他们可以塞进一个chunk里面。<code>Empty</code>之于<code>ByteString</code>就像<code>[]</code>之于list一样。</p><p><strong>unpack</strong>是<code>pack</code>的相反，他把一个bytestring变成一个byte list。</p><p><strong>fromChunks</strong>接受一串strict的bytestrings并把他变成一串lazy bytestring。<strong>toChunks</strong>接受一个lazy bytestrings并将他变成一串strict bytestrings。</p><pre class="code">ghci> B.fromChunks [S.pack [40,41,42], S.pack [43,44,45], S.pack [46,47,48]]  
Chunk "()*" (Chunk "+,-" (Chunk "./0" Empty))</pre><p>如果你有很多小的strict bytestrings而且不想先将他们join起来（会耗损memory）这样的作法是不错的。</p><p>bytestring版本的<code>:</code>叫做<strong>cons</strong>。他接受一个byte跟一个bytestring，并把这个byte放到bytestring的前端。他是lazy的操作，即使bytestring的第一个chunk不是满的，他也会新增一个chunk。这也是为什么当你要插入很多bytes的时候最好用strict版本的<code>cons</code>，也就是<strong>cons'</strong>。</p><pre class="code">ghci> B.cons 85 $ B.pack [80,81,82,84]  
Chunk "U" (Chunk "PQRT" Empty)  
ghci> B.cons' 85 $ B.pack [80,81,82,84]  
Chunk "UPQRT" Empty  
ghci> foldr B.cons B.empty [50..60]  
Chunk "2" (Chunk "3" (Chunk "4" (Chunk "5" (Chunk "6" (Chunk "7" (Chunk "8" (Chunk "9" (Chunk ":" (Chunk ";" (Chunk "<"  
Empty))))))))))  
ghci> foldr B.cons' B.empty [50..60]  
Chunk "23456789:;<" Empty</pre><p>你可以看到<strong>empty</strong>制造了一个空的bytestring。也注意到<code>cons</code>跟<code>cons'</code>的差异了吗？有了<code>foldr</code>，我们逐步地把一串数字从右边开始，一个个放到bytestring的前头。当我们用<code>cons</code>，我们则得到一个byte一个chunk的结果，并不是我们要的。</p><p>bytestring模块有一大票很像<code>Data.List</code>中的函数。包括了<code>head</code>，<code>tail</code>，<code>init</code>，<code>null</code>，<code>length</code>，<code>map</code>，<code>reverse</code>，<code>foldl</code>，<code>foldr</code>，<code>concat</code>，<code>takeWhile</code>，<code>filter</code>，等等。</p><p>他也有表现得跟<code>System.IO</code>中一样的函数，只有<code>Strings</code>被换成了<code>ByteString</code>而已。像是<code>System.IO</code>中的<code>readFile</code>，他的型态是<code>readFile :: FilePath -> IO String</code>，而bytestring模块中的<strong>readFile</strong>则是<code>readFile :: FilePath -> IO ByteString</code>。小心，如果你用了strict bytestring来读取一个文件，他会把文件内容都读进记忆体中。而使用lazy bytestring，他则会读取chunks。</p><p>让我们来写一个简单的程序，他从命令行接受两个文件名，然后拷贝第一个文件内容成第二个文件。虽然<code>System.Directory</code>中已经有一个函数叫<code>copyFile</code>，但我们想要实做自己的版本。</p><pre class="code">import System.Environment  
import qualified Data.ByteString.Lazy as B  
  
main = do  
    (fileName1:fileName2:_) <- getArgs  
    copyFile fileName1 fileName2  

copyFile :: FilePath -> FilePath -> IO ()  
copyFile source dest = do  
    contents <- B.readFile source  
    B.writeFile dest contents</pre><p>我们写了自己的函数，他接受两个<code>FilePath</code>（记住<code>FilePath</code>不过是<code>String</code>的同义词。）并回传一个I/O action，他会用bytestring拷贝第一个文件至另一个。在<code>main</code>函数中，我们做的只是拿到命令行引数然后调用那个函数来拿到一个I/O action。</p><pre class="code">$ runhaskell bytestringcopy.hs something.txt ../../something.txt</pre><p>就算我们不用bytestring来写，程序最后也会长得像这样。差别在于我们会用<code>B.readFile</code>跟<code>B.writeFile</code>而不是<code>readFile</code>跟<code>writeFile</code>。有很大的可能性，就是你只要import文件并在函数前加上qualified模块名，就可以把一个用正常String的程序改成用ByteString。也有可能你是要反过来做，但那也不难。</p><p>当你需要更好的性能来读取许多数据，尝试用bytestring，有很大的机会你会用很小的力气改进很多性能。我通常用正常String来写程序，然后在性能不好的时候把他们改成ByteString。</p><a name="Exceptions (例外)"></a><h2>Exceptions (例外)</h2><img src="/img/timber.png" style="float:left"></img><p>所有的编程语言都有要处里失败的情形。这就是人生。不同的语言有不同的处里方式。在C里面，我们通常用非正常范围的回传值（像是<code>-1</code>或null）来回传错误。Java跟C#则倾向于使用exception来处里失败的情况。当一个exception被丢出的时候，控制流程就会跳到我们做一些清理动作的地方，做完清理后exception被重新丢出，这样一些处里错误的代码可以完成他们的工作。</p><p>Haskell有一个很棒的型态系统。Algebraic data types允许像是<code>Maybe</code>或<code>Either</code>这种型态，我们能用这些型态来代表一些可能有或没有的结果。在C里面，在失败的时候回传<code>-1</code>是很常见的事。但他只对写程序的人有意义。如果我们不小心，我们有可能把这些错误码当作正常值来处里，便造成一些诨乱。Haskell的型态系统赋予我们更安全的环境。一个<code>a -> Maybe b</code>的函数指出了他会产生一个包含<code>b</code>的<code>Just</code>，或是回传<code>Nothing</code>。这型态跟<code>a -> b</code>是不同的，如果我们试着将两个函数混用，compiler便会警告我们。</p><p>尽管有表达力够强的型态来辅助失败的情形，Haskell仍然支持exception，因为exception在I/O的contexts下是比较合理的。在处里I/O的时候会有一堆奇奇怪怪的事情发生，环境是很不能被信赖的。像是打开文件。文件有可能被lock起来，也有可能文件被移除了，或是整个硬盘都被拔掉。所以直接跳到处里错误的代码是很合理的。</p><p>我们了解到I/O code会丢出exception是件合理的事。至于pure code呢？其实他也能丢出Exception。想想看<code>div</code>跟<code>head</code>两个案例。他们的型态是<code>(Integral a) => a -> a -> a</code>以及<code>[a] -> a</code>。<code>Maybe</code>跟<code>Either</code>都没有在他们的回传型态中，但他们都有可能失败。<code>div</code>有可能除以零，而<code>head</code>有可能你传给他一个空的list。</p><pre class="code">ghci> 4 `div` 0  
*** Exception: divide by zero  
ghci> head []  
*** Exception: Prelude.head: empty list</pre><img src="/img/police.png" style="float:left"></img><p>pure code能丢出Exception，但Exception只能在I/O section中被接到（也就是在<code>main</code>的do block中）这是因为在pure code中你不知道什么东西什么时候会被evaluate。因为lazy特性的缘故，程序没有一个特定的执行顺序，但I/O code有。</p><p>先前我们谈过为什么在I/O部份的程序要越少越好。程序的逻辑部份尽量都放在pure的部份，因为pure的特性就是他们的结果只会根据函数的参数不同而改变。当思考pure function的时候，你只需要考虑他回传什么，因为除此之外他不会有任何副作用。这会让事情简单许多。尽管I/O的部份是难以避免的（像是打开文件之类），但最好是把I/O部份降到最低。Pure functions缺省是lazy，那代表我们不知道他什么时候会被evaluate，不过我们也不该知道。然而，一旦pure functions需要丢出Exception，他们何时被evaluate就很重要了。那是因为我们只有在I/O的部份才能接到Exception。这很糟糕，因为我们说过希望I/O的部份越少越好。但如果我们不接Exception，我们的程序就会当掉。这问题有解决办法吗？答案是不要在pure code里面使用Exception。利用Haskell的型态系统，尽量使用<code>Either</code>或<code>Maybe</code>之类的型态来表示可能失败的计算。</p><p>这也是为什么我们要来看看怎么使用I/O Excetion。I/O Exception是当我们在<code>main</code>里面跟外界沟通失败而丢出的Exception。例如我们尝试打开一个文件，结果发现他已经被删掉或是其他状况。来看看一个尝试打开命令行引数所指定文件名称，并计算里面有多少行的程序。</p><pre class="code">import System.Environment  
import System.IO  
  
main = do (fileName:_) <- getArgs  
            contents <- readFile fileName  
            putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"</pre><p>一个很简单的程序。我们使用<code>getArgs</code>I/O action，并绑定第一个string到<code>fileName</code>。然后我们绑定文件内容到<code>contents</code>。最后，我们用<code>lines</code>来取得line的list，并计算list的长度，并用<code>show</code>来转换数字成string。他如我们想像的工作，但当我们给的文件名称不存在的时候呢？</p><pre class="code">$ runhaskell linecount.hs i_dont_exist.txt  
linecount.hs: i_dont_exist.txt: openFile: does not exist (No such file or directory)</pre><p>GHC丢了错误消息给我们，告诉我们文件不存在。然后程序就挂掉了。假如我们希望印出比较好一些的错误消息呢？一种方式就是在打开文件前检查他存不存在。用<code>System.Directory</code>中的<strong>doesFileExist</strong>。</p><pre class="code">import System.Environment  
import System.IO  
import System.Directory  
  
main = do (fileName:_) <- getArgs  
            fileExists <- doesFileExist fileName  
            if fileExists  
                then do contents <- readFile fileName  
                    putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  
                else do putStrLn "The file doesn't exist!"</pre><p>由于<code>doesFileExist</code>的型态是<code>doesFileExist :: FilePath -> IO Bool</code>，所以我们要写成<code>fileExists <- doesFileExist fileName</code>。那代表他回传含有一个布林值告诉我们文件存不存在的I/O action。<code>doesFileExist</code>是不能直接在if expression中使用的。</p><p>另一个解法是使用Exception。在这个情境下使用Exception是没问题的。文件不存在这个Exception是在I/O中被丢出，所以在I/O中接起来也没什么不对。</p><p>要这样使用Exception，我们必须使用<code>System.IO.Error</code>中的<strong>catch</strong>函数。他的型态是<code>catch :: IO a -> (IOError -> IO a) -> IO a</code>。他接受两个参数，第一个是一个I/O action。像是他可以接受一个打开文件的I/O action。第二个是handler。如果第一个参数的I/O action丢出了Exception，则他会被传给handler，他会决定要作些什么。所以整个I/O action的结果不是如预期中做完第一个参数的I/O action，就是handler处里的结果。</p><img src="/img/puppy.png" style="float:right"></img><p>如果你对其他语言像是Java, Python中try-catch的形式很熟，那<code>catch</code>其实跟他们很像。第一个参数就是其他语言中的try block。第二个参数就是其他语言中的catch block。其中handler只有在exception被丢出时才会被执行。</p><p>handler接受一个<code>IOError</code>型态的值，他代表的是一个I/O exception已经发生了。他也带有一些exception本身的信息。至于这型态在语言中使如何被实做则是要看编译器。这代表我们没办法用pattern matching的方式来查看<code>IOError</code>。就像我们不能用pattern matching来查看<code>IO something</code>的内容。但我们能用一些predicate来查看他们。</p><p>我们来看看一个展示<code>catch</code>的程序</p><pre class="code">import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  

toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
            contents <- readFile fileName  
            putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  

handler :: IOError -> IO ()  
handler e = putStrLn "Whoops, had some trouble!"</pre><p>首先你看到我们可以在关键字周围加上backticks来把<code>catch</code>当作infix function用，因为他刚好接受两个参数。这样使用让可读性变好。<code>toTry `catch` handler</code>跟<code>catch toTry handler</code>是一模一样的。<code>toTry</code>是一个I/O action，而<code>handler</code>接受一个<code>IOError</code>，并回传一个当exception发生时被执行的I/O action。</p><p>来看看执行的结果。</p><pre class="code">$ runhaskell count_lines.hs i_exist.txt  
The file has 3 lines!  
  
$ runhaskell count_lines.hs i_dont_exist.txt  
Whoops, had some trouble!</pre><p>在handler里面我们并没有检查我们拿到的是什么样的<code>IOError</code>，我们只是印出<code>"Whoops, had some trouble!"</code>。接住任何种类的Exception就跟其他语言一样，在Haskell中也不是一个好的习惯。假如其他种类的Exception发生了，好比说我们送一个中断指令，而我们没有接到的话会发生什么事？这就是为什么我们要做跟其他语言一样的事：就是检查我们拿到的是什么样的Exception。如果说是我们要的Exception，那就做对应的处里。如果不是，我们在重新丢出Exception。我们把我们的程序这样修改，只接住文件不存在的Exception。</p><pre class="code">import System.Environment  
import System.IO  
import System.IO.Error  
  
main = toTry `catch` handler  

toTry :: IO ()  
toTry = do (fileName:_) <- getArgs  
            contents <- readFile fileName  
            putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"  

handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | otherwise = ioError e</pre><p>除了handler以外其他东西都没变，我们只接住我们想要的I/O exception。这边使用了<code>System.IO.Error</code>中的函数<strong>isDoesNotExistError</strong>跟<strong>ioError</strong>。<code>isDoesNotExistError</code>是一个运作在<code>IOError</code>上的predicate ，他代表他接受一个<code>IOError</code>然后回传<code>True</code>或<code>False</code>，他的型态是<code>isDoesNotExistError :: IOError -> Bool</code>。我们用他来判断是否这个错误是文件不存在所造成的。我们这边使用guard，但其实也可以用if else。如果exception不是由于文件不存在所造成的，我们就用<code>ioEroror</code>重新丢出接到的exception。他的型态是<code>ioError :: IOException -> IO a</code>，所以他接受一个<code>IOError</code>然后产生一个会丢出exception的I/O action。那个I/O action的型态是<code>IO a</code>，但他其实不会产生任何结果，所以他可以被当作是<code>IO anything</code>。</p><p>所以有可能在<code>toTry</code>里面丢出的exception并不是文件不存在造成的，而<code>toTry `catch` handler</code>会接住再丢出来，很酷吧。</p><p>程序里面有好几个运作在<code>IOError</code>上的I/O action，当其中一个没有被evaluate成<code>True</code>时，就会掉到下一个guard。这些predicate分别为：</p><ol><li><p><strong>isAlreadyExistsError</strong></p></li><li><p><strong>isDoesNotExistError</strong></p></li><li><p><strong>isFullError</strong></p></li><li><p><strong>isEOFError</strong></p></li><li><p><strong>isIllegalOperation</strong></p></li><li><p><strong>isPermissionError</strong></p></li><li><p><strong>isUserError</strong></p></li></ol><p>大部分的意思都是显而易见的。当我们用了<strong>userError</strong>来丢出exception的时候，<code>isUserError</code>被evaluate成<code>True</code>。例如说，你可以写<code>ioError $ userError "remote computer unplugged!"</code>，尽管用<code>Either</code>或<code>Maybe</code>来表示可能的错误会比自己丢出exception更好。</p><p>所以你可能写一个像这样的handler</p><pre class="code">handler :: IOError -> IO ()  
handler e  
    | isDoesNotExistError e = putStrLn "The file doesn't exist!"  
    | isFullError e = freeSomeSpace  
    | isIllegalOperation e = notifyCops  
    | otherwise = ioError e</pre><p>其中<code>notifyCops</code>跟<code>freeSomeSpace</code>是一些你定义的I/O action。如果exception不是你要的，记得要把他们重新丢出，不然你的程序可能只会安静地当掉。</p><p><code>System.IO.Error</code>也会出了一些能询问exception性质的函数，像是是哪些handle造成错误，或哪些文件名造成错误。这些函数都是<code>ioe</code>当开头。而且你可以在文档中看到一整串详细数据。假设我们想要印出造成错误的文件名。我们不能直接印出从<code>getArgs</code>那边拿到的<code>fileName</code>，因为只有<code>IOError</code>被传进handler中，而handler并不知道其他事情。一个函数只依赖于他所被调用时的参数。这也是为什么我们会用<strong>ioeGetFileName</strong>这函数，他的型态是<code>ioeGetFileName :: IOError -> Maybe FilePath</code>。他接受一个<code>IOError</code>并回传一个<code>FilePath</code>（他是<code>String</code>的同义词。）基本上他做的事就是从<code>IOError</code>中抽出文件路径。我们来修改一下我们的程序。</p><pre class="code">import System.Environment     
import System.IO     
import System.IO.Error     
    
main = toTry `catch` handler     

toTry :: IO ()     
toTry = do (fileName:_) <- getArgs     
    contents <- readFile fileName     
    putStrLn $ "The file has " ++ show (length (lines contents)) ++ " lines!"     

handler :: IOError -> IO ()     
handler e     
    | isDoesNotExistError e =   
        case ioeGetFileName e of Just path -> putStrLn $ "Whoops! File does not exist at: " ++ path  
                                 Nothing -> putStrLn "Whoops! File does not exist at unknown location!"  
    | otherwise = ioError e</pre><p>在<code>isDoesNotExistError</code>是<code>True</code>的guard里面，我们在case expression中用<code>e</code>来调用<code>ioeGetFileName</code>，然后用pattern matching拆出<code>Maybe</code>中的值。当你想要用pattern matching却又不想要写一个新的函数的时候，case expression是你的好朋友。</p><p>你不想只用一个<code>catch</code>来接你I/O part中的所有exception。你可以只在特定地方用<code>catch</code>接exception，或你可以用不同的handler。像这样：</p><pre class="code">main = do toTry `catch` handler1  
          thenTryThis `catch` handler2  
          launchRockets</pre><p>这边<code>toTry</code>使用<code>handler1</code>当作handler，而<code>thenTryThis</code>用了<code>handler2</code>。<code>launchRockets</code>并不是<code>catch</code>的参数，所以如果有任何一个exception被丢出都会让我们的程序当掉，除非<code>launchRockets</code>使用<code>catch</code>来处里exception。当然<code>toTry</code>，<code>thenTryThis</code>跟<code>launchRockets</code>都是I/O actions，而且被do syntax绑在一起。这很像其他语言中的try-catch blocks，你可以把一小段程序用try-catch包住，你可以自己调整该包多少进去。</p><p>现在你知道如何处里I/O exception了。我们并没有提到如何从pure code中丢出exception，这是因为正如我们先前提到的，Haskell提供了更好的办法来处里错误。就算是在可能会失败的I/O action中，我也倾向用<code>IO (Either a b)</code>，代表他们是I/O action，但当他们被执行，他们结果的型态是<code>Either a b</code>，意思是不是<code>Left a</code>就是<code>Right b</code>。</p>
            <ul class="nav">
                <li class="left">  <img src="/img/prv.png"></img><a href="make-types-and-classes-for-ourselves.html">构造我们自己的Types和Typeclasses</a></li>
                <li class="center"><a href="chapters.html">Index</a></li>
                <li class="right"> <a href="functionally-solving-problems.html">函数式地思考来解决问题</a><img src="/img/nxt.png"></img></li>
            </ul>
        </div>
        <div id="footer">
        </div>
        <div id="feedback">Send feedback</div>
    </body>
</html>
